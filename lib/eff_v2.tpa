OUTER_SET EFF_V2_OPCODE_OFFSET = 0x10
OUTER_SET EFF_V2_TARGET_TYPE_OFFSET = 0x14
OUTER_SET EFF_V2_POWER_OFFSET = 0x18
OUTER_SET EFF_V2_PARAM_1_OFFSET = 0x1c
OUTER_SET EFF_V2_PARAM_2_OFFSET = 0x20
OUTER_SET EFF_V2_TIMING_MODE_OFFSET = 0x24
OUTER_SET EFF_V2_DURATION_OFFSET = 0x28
OUTER_SET EFF_V2_PROBABILITY_1_OFFSET = 0x2c
OUTER_SET EFF_V2_PROBABILITY_2_OFFSET = 0x2e
OUTER_SET EFF_V2_RESREF_OFFSET = 0x30
OUTER_SET EFF_V2_DISPEL_RESISTANCE_OFFSET = 0x5c
OUTER_SET EFF_V2_VARIABLE_NAME_OFFSET = 0xa8


DEFINE_PATCH_FUNCTION ALTER_EFF_V2_EFFECT
    INT_VAR opcode = ~~
        target_type = EFF_TARGET_PRESET
        power = 0
        parameter1 = 0
        parameter2 = 0
        timing_mode = EFF_TIMING_INSTANT_PERMANENT_UNTIL_DEATH
        dispel_resistance = 0
        duration = 0
        probability1 = 100
        probability2 = 0
    STR_VAR resource = ~~
        variable_name = ~~
BEGIN
    WRITE_LONG EFF_V2_OPCODE_OFFSET opcode
    WRITE_LONG EFF_V2_TARGET_TYPE_OFFSET target_type
    WRITE_LONG EFF_V2_POWER_OFFSET power
    WRITE_LONG EFF_V2_PARAM_1_OFFSET parameter1
    WRITE_LONG EFF_V2_PARAM_2_OFFSET parameter2
    WRITE_SHORT EFF_V2_TIMING_MODE_OFFSET timing_mode
    WRITE_LONG EFF_V2_DURATION_OFFSET duration
    WRITE_SHORT EFF_V2_PROBABILITY_1_OFFSET probability1
    WRITE_SHORT EFF_V2_PROBABILITY_2_OFFSET probability2
    WRITE_LONG EFF_V2_DISPEL_RESISTANCE_OFFSET dispel_resistance
    WRITE_ASCII EFF_V2_RESREF_OFFSET ~%resource%~ #8
    WRITE_ASCII EFF_V2_VARIABLE_NAME_OFFSET ~%variable_name%~ #32
END


DEFINE_PATCH_FUNCTION PATCH_EFF_V2_EFFECT
    STR_VAR patches_ref = ~~
BEGIN
    PATCH_IF (VARIABLE_IS_SET effect_opcode) BEGIN PATCH_FAIL ~Variable 'effect_opcode' leaks from outer scope.~ END
    PATCH_IF (VARIABLE_IS_SET effect_parameter1) BEGIN PATCH_FAIL ~Variable 'effect_parameter1' leaks from outer scope.~ END
    PATCH_IF (VARIABLE_IS_SET effect_parameter2) BEGIN PATCH_FAIL ~Variable 'effect_parameter2' leaks from outer scope.~ END
    PATCH_IF (VARIABLE_IS_SET effect_dispel_resistance) BEGIN PATCH_FAIL ~Variable 'effect_dispel_resistance' leaks from outer scope.~ END

    PHP_EACH ~%patches_ref%~ AS patch_name => patch_ref BEGIN

        PATCH_IF (
            VARIABLE_IS_SET $~%patch_ref%~(match_opcode) AND
            NOT VARIABLE_IS_SET effect_opcode
        ) BEGIN
            READ_LONG EFF_V2_OPCODE_OFFSET effect_opcode        
        END

        PATCH_IF (
            NOT VARIABLE_IS_SET $~%patch_ref%~(match_opcode) OR effect_opcode == $~%patch_ref%~(match_opcode)
        ) BEGIN

            PATCH_IF (
                VARIABLE_IS_SET $~%patch_ref%~(match_parameter1) AND
                NOT VARIABLE_IS_SET effect_parameter1
            ) BEGIN
                READ_LONG EFF_V2_PARAM_1_OFFSET effect_parameter1
            END
            PATCH_IF (
                VARIABLE_IS_SET $~%patch_ref%~(match_parameter2) AND
                NOT VARIABLE_IS_SET effect_parameter2
            ) BEGIN
                READ_LONG EFF_V2_PARAM_2_OFFSET effect_parameter2
            END
            PATCH_IF (
                VARIABLE_IS_SET $~%patch_ref%~(match_dispel_resistance) AND
                NOT VARIABLE_IS_SET effect_dispel_resistance
            ) BEGIN
                READ_LONG EFF_V2_DISPEL_RESISTANCE_OFFSET effect_dispel_resistance
            END

            PATCH_IF (
                (NOT VARIABLE_IS_SET $~%patch_ref%~(match_parameter1) OR effect_parameter1 == $~%patch_ref%~(match_parameter1)) AND
                (NOT VARIABLE_IS_SET $~%patch_ref%~(match_parameter2) OR effect_parameter2 == $~%patch_ref%~(match_parameter2)) AND
                (NOT VARIABLE_IS_SET $~%patch_ref%~(match_dispel_resistance) OR effect_dispel_resistance == $~%patch_ref%~(match_dispel_resistance))
            ) BEGIN
                PATCH_PRINT ~Patching %SOURCE_EXT% %SOURCE_RES% (%patch_name%)~                
                
                PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(opcode)) BEGIN
                    WRITE_LONG EFF_V2_OPCODE_OFFSET $~%patch_ref%~(opcode) 
                END
                
                PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(parameter1)) BEGIN
                    WRITE_LONG EFF_V2_PARAM_1_OFFSET $~%patch_ref%~(parameter1)
                END
                
                PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(parameter2)) BEGIN
                    WRITE_LONG EFF_V2_PARAM_2_OFFSET $~%patch_ref%~(parameter2)
                END
                
                PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(dispel_resistance)) BEGIN
                    WRITE_LONG EFF_V2_DISPEL_RESISTANCE_OFFSET $~%patch_ref%~(dispel_resistance)
                END
                
                PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(resource)) BEGIN
                    SPRINT resource $~%patch_ref%~(resource)
                    WRITE_ASCII EFF_V2_RESREF_OFFSET ~%resource%~ #8
                END
            END
        END
    END
END
