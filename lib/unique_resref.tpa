/**
 * This is alternative version of WeiDU GET_UNIQUE_FILE_NAME function
 * See: https://github.com/WeiDUorg/weidu/blob/111b111/src/tph/include/get_unique_file_name.tpa
 * It works faster on bigger datasets and allows to provide custom namespace
 */
DEFINE_DIMORPHIC_FUNCTION UNIQUE_RESREF
  STR_VAR namespace = ~%MOD_PREFIX%_~
          key = ~~
  RET resref
  BEGIN
    OUTER_SPRINT ids_name ~%namespace%_UNIQUE_RESREF~
    OUTER_SPRINT ids_file ~%ids_name%.IDS~

    ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%ids_file%~) BEGIN
        COPY + ~.../blank~ ~override/%ids_file%~
    END

    OUTER_SET exists = IDS_OF_SYMBOL (~%ids_name%~ ~%key%~)
    OUTER_SET tail = 0

    ACTION_IF (exists = "-1") BEGIN 
        OUTER_SET exists = 0

        COPY_EXISTING - ~%ids_file%~ ~~
            COUNT_2DA_ROWS 1 tail
            BUT_ONLY
    END

    OUTER_SET value = exists ? exists : tail + 1

    ACTION_IF (NOT exists) BEGIN 
        APPEND + ~%ids_file%~ ~%value% %key%~
    END

    LAF ~MATH_BASE36~ INT_VAR base10 = value RET base36 END
    OUTER_SPRINT resref ~%namespace%%base36%~
    
    ACTION_IF (STRING_LENGTH ~%resref%~ > 8) BEGIN 
        FAIL ~UNIQUE_RESREF exceeds namespace limit: %resref% (%tail%)~
    END
END
