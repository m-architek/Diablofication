OUTER_SET EFF_OPCODE_CURE_SLEEP = 2
OUTER_SET EFF_OPCODE_CURE_BERSERK = 4
OUTER_SET EFF_OPCODE_CHARM = 5
OUTER_SET EFF_OPCODE_CURE_POISON = 11
OUTER_SET EFF_OPCODE_HP_DAMAGE = 12
OUTER_SET EFF_OPCODE_INSTANT_DEATH = 13
OUTER_SET EFF_OPCODE_HP_CURRENT = 17
OUTER_SET EFF_OPCODE_HP_MAXIMUM = 18
OUTER_SET EFF_OPCODE_INT_MOD = 19
OUTER_SET EFF_OPCODE_STAT_MORALE = 23
OUTER_SET EFF_OPCODE_RAISE_DEAD = 32
OUTER_SET EFF_OPCODE_CURE_STUN = 46
OUTER_SET EFF_OPCODE_CURE_INVISIBILITY = 47
OUTER_SET EFF_OPCODE_CURE_SILENCE = 48
OUTER_SET EFF_OPCODE_KILL_CREATURE = 55
OUTER_SET EFF_OPCODE_DISPEL_MAGIC = 58
OUTER_SET EFF_OPCODE_GRAPHICS_COLOR_FADE = 61
OUTER_SET EFF_OPCODE_CURE_BLINDENESS = 75
OUTER_SET EFF_OPCODE_CURE_FEEBLEMINDEDNESS = 77
OUTER_SET EFF_OPCODE_CURE_DISEASE = 79
OUTER_SET EFF_OPCODE_CURE_DEAFNESS = 81
OUTER_SET EFF_OPCODE_PROTECTION_FROM_OPCODE = 101
OUTER_SET EFF_OPCODE_STATE_PETRIFICATION = 134
OUTER_SET EFF_OPCODE_DISPLAY_STRING = 139
OUTER_SET EFF_OPCODE_DISPLAY_ICON = 142
OUTER_SET EFF_OPCODE_SUMMON_REPLACE_CREATURE = 151
OUTER_SET EFF_OPCODE_CURE_HORROR = 161
OUTER_SET EFF_OPCODE_CURE_HOLD = 162
OUTER_SET EFF_OPCODE_CURE_DRUNKENESS = 164
OUTER_SET EFF_OPCODE_USE_EFF_FILE = 177
OUTER_SET EFF_OPCODE_PROTECTION_FROM_SPELL = 206
OUTER_SET EFF_OPCODE_HP_MINIMUM = 208
OUTER_SET EFF_OPCODE_KILL_60HP = 209
OUTER_SET EFF_OPCODE_MAZE = 213
OUTER_SET EFF_OPCODE_GRPAHICS_PLAY_EFFECT = 215
OUTER_SET EFF_OPCODE_LEVEL_DRAIN = 216
OUTER_SET EFF_OPCODE_CURE_LEVEL_DRAIN = 224
OUTER_SET EFF_OPCODE_DISINTEGRATE = 238
OUTER_SET EFF_OPCODE_REMOVE_ICON = 240
OUTER_SET EFF_OPCODE_ALTERNATIVE_CHARM = 241
OUTER_SET EFF_OPCODE_CURE_CONFUSION = 242
OUTER_SET EFF_OPCODE_CURE_PAUSE = 270
OUTER_SET EFF_OPCODE_GRAPHICS_REMOVE_SELECTION_CIRCLE = 287
OUTER_SET EFF_OPCODE_GRAPHICS_DISABLE_PERMANENT_DEATH = 295
OUTER_SET EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR = 309
OUTER_SET EFF_OPCODE_SPELL_MAGICAL_REST = 316
OUTER_SET EFF_OPCODE_REMOVE_EFFECTS_BY_RESOURCE = 321
OUTER_SET EFF_OPCODE_PROTECTION_FROM_RESOURCE_AND_MESSAGE = 324
OUTER_SET EFF_OPCODE_APPLY_EFFECTS_LIST = 326
OUTER_SET EFF_OPCODE_REMOVE_OPCODE = 337
OUTER_SET EFF_OPCODE_MINIMUM_BASE_STATS = 367

OUTER_SET EFF_TARGET_SELF = 1
OUTER_SET EFF_TARGET_PRESET = 2

OUTER_SET EFF_TIMING_INSTANT_LIMITED = 0
OUTER_SET EFF_TIMING_INSTANT_PERMANENT_UNTIL_DEATH = 1
OUTER_SET EFF_TIMING_INSTANT_PERMANENT = 9

OUTER_SET EFF_NATURAL_NONMAGICAL = 0
OUTER_SET EFF_DISPELLABLE_BLOCKABLE = 1
OUTER_SET EFF_UNDISPELLABLE_UNBLOCKABLE = 2
OUTER_SET EFF_DISPELLABLE_UNBLOCKABLE = 3


DEFINE_PATCH_FUNCTION PATCH_EFF_EFFECT_LIST
    INT_VAR offset_shift = ~~ 
        effects_count = ~~
    STR_VAR patches_ref = 0
        eff_strategy_ref = 0
    RET effects_count_diff
BEGIN 
    SET effects_count_diff = 0

    FOR (effect_index = 0; effect_index < effects_count; ++effect_index) BEGIN
        SET effect_offset = offset_shift + (effect_index * $~%eff_strategy_ref%~(size))

        LPF PATCH_EFF_EFFECT INT_VAR offset_shift = effect_offset
            STR_VAR patches_ref eff_strategy_ref
            RET patch_effects_count_diff = effects_count_diff
            END

        SET effect_index = effect_index + patch_effects_count_diff
        SET effects_count = effects_count + patch_effects_count_diff
        SET effects_count_diff = effects_count_diff + patch_effects_count_diff
    END
END

DEFINE_PATCH_FUNCTION PATCH_EFF_EFFECT
    INT_VAR offset_shift = ~~
    STR_VAR patches_ref = 0
        eff_strategy_ref = 0
    RET effects_count_diff
    RET_ARRAY created_eff_resources
BEGIN    
    CLEAR_ARRAY created_eff_resources

    LPF PATCH_EFF_EFFECT_FILTER_APPLICABLE_PATCHES INT_VAR offset_shift
        STR_VAR patches_ref eff_strategy_ref
        RET_ARRAY applicable_patches
        END

    SET effects_count_diff = 0
    PHP_EACH applicable_patches AS patch_name => patch_ref BEGIN
        LPF PATCH_EFF_EFFECT_APPLY_PATCH INT_VAR offset_shift effects_count_diff
            STR_VAR patch_name patch_ref eff_strategy_ref created_eff_resources_ref = created_eff_resources
            RET patch_effects_count_diff = effects_count_diff
            RET_ARRAY created_eff_resources
            END

        SET offset_shift = offset_shift + (patch_effects_count_diff * $~%eff_strategy_ref%~(size))
        SET effects_count_diff = effects_count_diff + patch_effects_count_diff
    END
END

DEFINE_PATCH_FUNCTION PATCH_EFF_EFFECT_FILTER_APPLICABLE_PATCHES
    INT_VAR offset_shift = ~~
    STR_VAR patches_ref = 0 eff_strategy_ref = 0
    RET_ARRAY applicable_patches
BEGIN
    PATCH_IF (VARIABLE_IS_SET effect_opcode) BEGIN PATCH_FAIL ~Variable 'effect_opcode' leaks from outer scope.~ END
    PATCH_IF (VARIABLE_IS_SET effect_parameter1) BEGIN PATCH_FAIL ~Variable 'effect_parameter1' leaks from outer scope.~ END
    PATCH_IF (VARIABLE_IS_SET effect_parameter2) BEGIN PATCH_FAIL ~Variable 'effect_parameter2' leaks from outer scope.~ END
    PATCH_IF (VARIABLE_IS_SET effect_dispel_resistance) BEGIN PATCH_FAIL ~Variable 'effect_dispel_resistance' leaks from outer scope.~ END
    PATCH_IF (VARIABLE_IS_SET effect_min_level) BEGIN PATCH_FAIL ~Variable 'effect_min_level' leaks from outer scope.~ END

    CLEAR_ARRAY applicable_patches

    SPRINT eff_read_strategy_ref $~%eff_strategy_ref%~(read_strategy) 

    PHP_EACH ~%patches_ref%~ AS patch_name => patch_ref BEGIN
        PATCH_IF (
            NOT VARIABLE_IS_SET $~%patch_ref%~(match_opcode) AND
            NOT VARIABLE_IS_SET $~%patch_ref%~(match_resource)
        ) BEGIN 
            PATCH_FAIL ~At least one of 'match_opcode' and 'match_resource' is required, but missing for patch %patch_name%.~ 
        END

        PATCH_IF (
            VARIABLE_IS_SET $~%patch_ref%~(match_opcode) AND
            NOT VARIABLE_IS_SET effect_opcode
        ) BEGIN
            SPRINT read_opcode_fn $~%eff_read_strategy_ref%~(opcode)
            LPF ~%read_opcode_fn%~ INT_VAR offset_shift RET effect_opcode = opcode END
        END

        PATCH_IF (NOT VARIABLE_IS_SET $~%patch_ref%~(match_opcode) OR effect_opcode == $~%patch_ref%~(match_opcode)) BEGIN

            PATCH_IF (
                VARIABLE_IS_SET $~%patch_ref%~(match_resource) AND
                NOT VARIABLE_IS_SET effect_resource
            ) BEGIN
                SPRINT read_resource_fn $~%eff_read_strategy_ref%~(resource)
                LPF ~%read_resource_fn%~ INT_VAR offset_shift RET effect_resource = resource END
            END

            PATCH_IF (NOT VARIABLE_IS_SET $~%patch_ref%~(match_resource) OR ~%effect_resource%~ STR_EQ $~%patch_ref%~(match_resource)) BEGIN

                PATCH_IF (
                    VARIABLE_IS_SET $~%patch_ref%~(match_parameter1) AND
                    NOT VARIABLE_IS_SET effect_parameter1
                ) BEGIN
                    SPRINT read_parameter1_fn $~%eff_read_strategy_ref%~(parameter1) 
                    LPF ~%read_parameter1_fn%~ INT_VAR offset_shift RET effect_parameter1 = parameter1 END
                END
                PATCH_IF (
                    VARIABLE_IS_SET $~%patch_ref%~(match_parameter2) AND
                    NOT VARIABLE_IS_SET effect_parameter2
                ) BEGIN
                    SPRINT read_parameter2_fn $~%eff_read_strategy_ref%~(parameter2)
                    LPF ~%read_parameter2_fn%~ INT_VAR offset_shift RET effect_parameter2 = parameter2 END
                END
                PATCH_IF (
                    VARIABLE_IS_SET $~%patch_ref%~(match_dispel_resistance) AND
                    NOT VARIABLE_IS_SET effect_dispel_resistance
                ) BEGIN
                    SPRINT read_dispel_resistance_fn $~%eff_read_strategy_ref%~(dispel_resistance)
                    LPF ~%read_dispel_resistance_fn%~ INT_VAR offset_shift RET effect_dispel_resistance = dispel_resistance END
                END
                PATCH_IF (
                    VARIABLE_IS_SET $~%patch_ref%~(match_min_level) AND
                    NOT VARIABLE_IS_SET effect_min_level
                ) BEGIN
                    SPRINT read_min_level_fn $~%eff_read_strategy_ref%~(min_level)
                    LPF ~%read_min_level_fn%~ INT_VAR offset_shift RET effect_min_level = min_level END
                END

                PATCH_IF (
                    (NOT VARIABLE_IS_SET $~%patch_ref%~(match_parameter1) OR effect_parameter1 == $~%patch_ref%~(match_parameter1)) AND
                    (NOT VARIABLE_IS_SET $~%patch_ref%~(match_parameter2) OR effect_parameter2 == $~%patch_ref%~(match_parameter2)) AND
                    (NOT VARIABLE_IS_SET $~%patch_ref%~(match_dispel_resistance) OR effect_dispel_resistance == $~%patch_ref%~(match_dispel_resistance)) AND
                    (NOT VARIABLE_IS_SET $~%patch_ref%~(match_min_level) OR effect_min_level == $~%patch_ref%~(match_min_level))
                ) BEGIN
                    DEFINE_ASSOCIATIVE_ARRAY applicable_patches BEGIN
                        ~%patch_name%~ => ~%patch_ref%~
                    END
                END
            END
        END        
    END
END

DEFINE_PATCH_FUNCTION PATCH_EFF_EFFECT_APPLY_PATCH
    INT_VAR offset_shift = ~~ effects_count_diff = 0
    STR_VAR patch_name = 0 patch_ref = 0 eff_strategy_ref = 0 created_eff_resources_ref = 0
    RET effects_count_diff
    RET_ARRAY ~%created_eff_resources_ref%~
BEGIN 
    PATCH_PRINT ~Patching %SOURCE_EXT% %SOURCE_RES% (%patch_name%)~

    PATCH_MATCH $~%patch_ref%~(patch_type) WITH
        ~CLONE~ BEGIN
            LPF PATCH_EFF_EFFECT_APPLY_PATCH_CLONE INT_VAR offset_shift
                STR_VAR patch_name patch_ref eff_strategy_ref created_eff_resources_ref
                RET effects_count_diff
                RET_ARRAY ~%created_eff_resources_ref%~
                END
        END
        DEFAULT
            LPF PATCH_EFF_EFFECT_APPLY_PATCH_WRITE INT_VAR offset_shift
                STR_VAR patch_name patch_ref eff_strategy_ref
                END

            SET effects_count_diff = 0
    END
END

DEFINE_PATCH_FUNCTION PATCH_EFF_EFFECT_APPLY_PATCH_CLONE
    INT_VAR offset_shift = ~~
    STR_VAR patch_name = 0 patch_ref = 0 eff_strategy_ref = 0 created_eff_resources_ref = 0
    RET effects_count_diff
    RET_ARRAY ~%created_eff_resources_ref%~
BEGIN 
    PATCH_PRINT ~ ! Clone in %SOURCE_EXT% %SOURCE_RES% (%patch_name%)~ 
    
    PATCH_MATCH ~%SOURCE_EXT%~ WITH
        ~EFF~ BEGIN
            INNER_ACTION BEGIN
                LAF GET_UNIQUE_FILE_NAME STR_VAR extension = ~%SOURCE_EXT%~
                    RET filename
                    END
                
                LOG ~Clone %SOURCE_EXT% %SOURCE_RES% as %filename%.%SOURCE_EXT% (%patch_name%)~ 

                COPY_EXISTING ~%SOURCE_RES%.%SOURCE_EXT%~ ~override/%filename%.%SOURCE_EXT%~
                    LPF PATCH_EFF_EFFECT_APPLY_PATCH_WRITE INT_VAR offset_shift = 0
                        STR_VAR patch_name patch_ref eff_strategy_ref
                        END
            END

            DEFINE_ASSOCIATIVE_ARRAY ~%created_eff_resources_ref%~ BEGIN 
                ~%SOURCE_RES%~, ~%filename%~ => ~%patch_name%~
            END

            SET effects_count_diff = 0
        END
        DEFAULT
            SET eff_size = $~%eff_strategy_ref%~(size)

            READ_ASCII offset_shift effect_content (eff_size)
            INSERT_BYTES offset_shift eff_size
            WRITE_ASCII offset_shift ~%effect_content%~

            LPF PATCH_EFF_EFFECT_APPLY_PATCH_WRITE INT_VAR offset_shift
                STR_VAR patch_name patch_ref eff_strategy_ref
                END

            SET effects_count_diff = 1
    END
END

DEFINE_PATCH_FUNCTION PATCH_EFF_EFFECT_APPLY_PATCH_WRITE
    INT_VAR offset_shift = ~~
    STR_VAR patch_name = 0 patch_ref = 0 eff_strategy_ref = 0
BEGIN            
    SPRINT eff_write_strategy_ref $~%eff_strategy_ref%~(write_strategy) 

    PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(opcode)) BEGIN
        SPRINT write_opcode_fn $~%eff_write_strategy_ref%~(opcode)
        LPF ~%write_opcode_fn%~ INT_VAR offset_shift opcode = $~%patch_ref%~(opcode) END
    END
    
    PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(parameter1)) BEGIN
        SPRINT write_parameter1 $~%eff_write_strategy_ref%~(parameter1)
        LPF ~%write_parameter1%~ INT_VAR offset_shift parameter1 = $~%patch_ref%~(parameter1) END
    END
    
    PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(parameter2)) BEGIN
        SPRINT write_parameter2_fn $~%eff_write_strategy_ref%~(parameter2)
        LPF ~%write_parameter2_fn%~ INT_VAR offset_shift parameter2 = $~%patch_ref%~(parameter2) END
    END
    
    PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(dispel_resistance)) BEGIN
        SPRINT write_dispel_resistance_fn $~%eff_write_strategy_ref%~(dispel_resistance)
        LPF ~%write_dispel_resistance_fn%~ INT_VAR offset_shift dispel_resistance = $~%patch_ref%~(dispel_resistance) END
    END

    PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(max_level)) BEGIN
        SPRINT write_max_level_fn $~%eff_write_strategy_ref%~(max_level)
        LPF ~%write_max_level_fn%~ INT_VAR offset_shift max_level = $~%patch_ref%~(max_level) END
    END
    
    PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(min_level)) BEGIN
        SPRINT write_min_level_fn $~%eff_write_strategy_ref%~(min_level)
        LPF ~%write_min_level_fn%~ INT_VAR offset_shift min_level = $~%patch_ref%~(min_level) END
    END
    
    PATCH_IF (VARIABLE_IS_SET $~%patch_ref%~(resource)) BEGIN
        SPRINT write_resource_fn $~%eff_write_strategy_ref%~(resource)
        LPF ~%write_resource_fn%~ INT_VAR offset_shift STR_VAR resource = $~%patch_ref%~(resource) END
    END  
END
