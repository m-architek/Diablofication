OUTER_SET SPL_ABILITY_SIZE = 40

OUTER_SET SPL_ABILITY_TYPE_OFFSET = 0x2
OUTER_SET SPL_ABILITY_TYPE_NONE = 0
OUTER_SET SPL_ABILITY_TYPE_SPELL = 2
OUTER_SET SPL_ABILITY_TYPE_INNATE = 4


DEFINE_PATCH_FUNCTION ADD_SPL_ABILITY
    INT_VAR ability_type = SPL_ABILITY_TYPE_NONE
    RET ability_index
BEGIN
    READ_LONG ITM_SPL_EFFECTS_OFFSET effects_offset
    READ_LONG ITM_SPL_ABILITIES_OFFSET abilities_offset
    READ_SHORT ITM_SPL_ABILITIES_COUNT_OFFSET abilities_count
    READ_SHORT ITM_SPL_GLOBAL_EFFECTS_INDEX_OFFSET global_effects_index
    READ_SHORT ITM_SPL_GLOBAL_EFFECTS_COUNT_OFFSET global_effects_count
    SET are_abilities_after_effects = abilities_offset > effects_offset
    SET are_global_effects_after_ability_effects = global_effects_index != 0

    SET ability_index = abilities_count
    SET ability_offset = abilities_offset + (ability_index * SPL_ABILITY_SIZE)

    INSERT_BYTES ability_offset SPL_ABILITY_SIZE
    WRITE_SHORT (ability_offset + SPL_ABILITY_TYPE_OFFSET) ability_type

    WRITE_SHORT ITM_SPL_ABILITIES_COUNT_OFFSET (abilities_count + 1)
    PATCH_IF (NOT are_abilities_after_effects) BEGIN 
        WRITE_LONG ITM_SPL_EFFECTS_OFFSET (effects_offset + SPL_ABILITY_SIZE)
    END
END


DEFINE_PATCH_FUNCTION ADD_SPL_ABILITY_EFFECT
    INT_VAR ability_index = ~~
        opcode = ~~
        target = EFF_TARGET_PRESET
        power = 0
        parameter1 = 0
        parameter2 = 0
        timing = EFF_TIMING_INSTANT_PERMANENT_UNTIL_DEATH
        dispel_resistance = EFF_NATURAL_NONMAGICAL
        duration = 0
        probability1 = 100
        probability2 = 0
        max_level = 0
        min_level = 0
    STR_VAR resource = ~~
    RET ability_effect_index
BEGIN
    READ_LONG ITM_SPL_EFFECTS_OFFSET effects_offset
    READ_LONG ITM_SPL_ABILITIES_OFFSET abilities_offset
    READ_SHORT ITM_SPL_ABILITIES_COUNT_OFFSET abilities_count
    READ_SHORT ITM_SPL_GLOBAL_EFFECTS_INDEX_OFFSET global_effects_index
    READ_SHORT ITM_SPL_GLOBAL_EFFECTS_COUNT_OFFSET global_effects_count
    SET are_abilities_after_effects = abilities_offset > effects_offset
    SET are_global_effects_after_ability_effects = global_effects_index != 0

    SET ability_offset = abilities_offset + ability_index * SPL_ABILITY_SIZE
    READ_SHORT (ability_offset + ITM_SPL_ABILITY_EFFECTS_COUNT_OFFSET) ability_effects_count
    READ_SHORT (ability_offset + ITM_SPL_ABILITY_EFFECTS_INDEX_OFFSET) ability_effects_index

    SET ability_effect_index = ability_effects_count
    SET ability_effect_offset = effects_offset + (ability_effects_index + ability_effect_index) * EFF_V1_SIZE
    
    INSERT_BYTES ability_effect_offset EFF_V1_SIZE
    LPF WRITE_EFF_V1_OPCODE INT_VAR offset_shift = ability_effect_offset opcode END
    LPF WRITE_EFF_V1_TARGET INT_VAR offset_shift = ability_effect_offset target END
    LPF WRITE_EFF_V1_POWER INT_VAR offset_shift = ability_effect_offset power END
    LPF WRITE_EFF_V1_PARAMETER1 INT_VAR offset_shift = ability_effect_offset parameter1 END
    LPF WRITE_EFF_V1_PARAMETER2 INT_VAR offset_shift = ability_effect_offset parameter2 END
    LPF WRITE_EFF_V1_TIMING_MODE INT_VAR offset_shift = ability_effect_offset timing_mode = timing END
    LPF WRITE_EFF_V1_DISPEL_RESISTANCE INT_VAR offset_shift = ability_effect_offset dispel_resistance END
    LPF WRITE_EFF_V1_DURATION INT_VAR offset_shift = ability_effect_offset duration END
    LPF WRITE_EFF_V1_PROBABILITY1 INT_VAR offset_shift = ability_effect_offset probability1 END
    LPF WRITE_EFF_V1_PROBABILITY2 INT_VAR offset_shift = ability_effect_offset probability2 END
    LPF WRITE_EFF_V1_MAX_LEVEL INT_VAR offset_shift = ability_effect_offset max_level END
    LPF WRITE_EFF_V1_MIN_LEVEL INT_VAR offset_shift = ability_effect_offset min_level END
    LPF WRITE_EFF_V1_RESOURCE INT_VAR offset_shift = ability_effect_offset STR_VAR resource END

    LPF ADJUST_ITM_SPL_AFTER_ABILITY_EFFECTS_COUNT_CHANGE 
        INT_VAR effects_count_diff = 1
            abilities_offset
            abilities_count
            global_effects_index
            ability_size = SPL_ABILITY_SIZE
            ability_offset
            ability_index
            are_abilities_after_effects
            are_global_effects_after_ability_effects
        RET abilities_offset global_effects_index
        END
END


DEFINE_PATCH_FUNCTION PATCH_SPL_EFFECTS
    STR_VAR patches_ref = 0
BEGIN
    LPF PATCH_ITM_SPL_EFFECTS
        INT_VAR ability_size = SPL_ABILITY_SIZE
        STR_VAR patches_ref
        END
END
