OUTER_SET SPL_SOUND_OFFSET = 0x10	
OUTER_SET SPL_SPELL_TYPE_OFFSET = 0x1c
OUTER_SET SPL_EXCLUSION_OFFSET = 0x1e
OUTER_SET SPL_ANIMATION_OFFSET = 0x22	
OUTER_SET SPL_SCHOOL_OFFSET = 0x25
OUTER_SET SPL_SECONDARY_TYPE_OFFSET = 0x27
OUTER_SET SPL_LEVEL_OFFSET = 0x34
OUTER_SET SPL_ICON_OFFSET = 0x3a

OUTER_SET SPL_SPELL_TYPE_SPECIAL = 0
OUTER_SET SPL_SPELL_TYPE_WIZARD = 1
OUTER_SET SPL_SPELL_TYPE_PRIEST = 2
OUTER_SET SPL_SPELL_TYPE_PSIONIC = 3
OUTER_SET SPL_SPELL_TYPE_INNATE = 4
OUTER_SET SPL_SPELL_TYPE_SONG = 5

OUTER_SET SPL_EXCLUSION_ABJURERS = BIT6	

OUTER_SET SPL_ANIMATION_ALTERATION = 10

OUTER_SET SPL_SCHOOL_TRANSMUTER = 8

OUTER_SET SPL_SECONDARY_TYPE_NON_COMBAT = 13

OUTER_SET SPL_ABILITY_SIZE = 40
OUTER_SET SPL_ABILITY_TYPE_OFFSET = 0x0
OUTER_SET SPL_ABILITY_LOCATION_OFFSET = 0x2
OUTER_SET SPL_ABILITY_ICON_OFFSET = 0x4
OUTER_SET SPL_ABILITY_TARGET_OFFSET = 0xc
OUTER_SET SPL_ABILITY_RANGE_OFFSET = 0xe
OUTER_SET SPL_ABILITY_LEVEL_REQUIRED_OFFSET = 0x10
OUTER_SET SPL_ABILITY_CASTING_TIME_OFFSET = 0x12


DEFINE_PATCH_FUNCTION ADD_SPL_HEADER 
    INT_VAR spell_type = 0
        exclusion = 0
        animation = 0
        school = 0
        secondary_type = 0
        level = 0
    STR_VAR sound = ~~
        icon = ~~
BEGIN 
    WRITE_ASCII SPL_SOUND_OFFSET ~%sound%~ #8 
    WRITE_SHORT SPL_SPELL_TYPE_OFFSET spell_type
    WRITE_LONG SPL_EXCLUSION_OFFSET exclusion
    WRITE_SHORT SPL_ANIMATION_OFFSET animation
    WRITE_BYTE SPL_SCHOOL_OFFSET school
    WRITE_BYTE SPL_SECONDARY_TYPE_OFFSET secondary_type
    WRITE_LONG SPL_LEVEL_OFFSET level
    WRITE_ASCII SPL_ICON_OFFSET ~%icon%~ #8 
END


DEFINE_PATCH_FUNCTION ADD_SPL_ABILITY
    INT_VAR type = ITM_SPL_ABILITY_TYPE_MELEE
        location = ITM_SPL_ABILITY_LOCATION_NONE
        target = 0
        range = 0
        level_required = 0
        casting_time = 0
    STR_VAR icon = ~~
    RET ability_index
BEGIN
    READ_LONG ITM_SPL_EFFECTS_OFFSET effects_offset
    READ_LONG ITM_SPL_ABILITIES_OFFSET abilities_offset
    READ_SHORT ITM_SPL_ABILITIES_COUNT_OFFSET abilities_count
    READ_SHORT ITM_SPL_GLOBAL_EFFECTS_INDEX_OFFSET global_effects_index
    READ_SHORT ITM_SPL_GLOBAL_EFFECTS_COUNT_OFFSET global_effects_count
    SET are_abilities_after_effects = abilities_offset > effects_offset

    SET ability_index = abilities_count
    SET ability_offset = abilities_offset + (ability_index * SPL_ABILITY_SIZE)

    INSERT_BYTES ability_offset SPL_ABILITY_SIZE
    WRITE_BYTE (ability_offset + SPL_ABILITY_TYPE_OFFSET) type
    WRITE_SHORT (ability_offset + SPL_ABILITY_LOCATION_OFFSET) location
    WRITE_ASCII (ability_offset + SPL_ABILITY_ICON_OFFSET) ~%icon%~ #8 
    WRITE_BYTE (ability_offset + SPL_ABILITY_TARGET_OFFSET) target
    WRITE_SHORT (ability_offset + SPL_ABILITY_RANGE_OFFSET) range
    WRITE_SHORT (ability_offset + SPL_ABILITY_LEVEL_REQUIRED_OFFSET) level_required
    WRITE_SHORT (ability_offset + SPL_ABILITY_CASTING_TIME_OFFSET) casting_time

    LPF ADJUST_ITM_SPL_NEW_ABILITY_EFFECTS_INDEX 
        INT_VAR abilities_offset
            ability_size = SPL_ABILITY_SIZE
            ability_index
            ability_offset
        END

    WRITE_SHORT ITM_SPL_ABILITIES_COUNT_OFFSET (abilities_count + 1)
    PATCH_IF (NOT are_abilities_after_effects) BEGIN 
        WRITE_LONG ITM_SPL_EFFECTS_OFFSET (effects_offset + SPL_ABILITY_SIZE)
    END
END


DEFINE_PATCH_FUNCTION ADD_SPL_ABILITY_EFFECT
    INT_VAR ability_index = ~~
        opcode = ~~
        target = EFF_TARGET_PRESET
        power = 0
        parameter1 = 0
        parameter2 = 0
        timing = EFF_TIMING_INSTANT_PERMANENT_UNTIL_DEATH
        dispel_resistance = EFF_NATURAL_NONMAGICAL
        duration = 0
        probability1 = 100
        probability2 = 0
        max_level = 0
        min_level = 0
    STR_VAR resource = ~~
BEGIN
    LPF ADD_ITM_SPL_ABILITY_EFFECT
        INT_VAR ability_size = SPL_ABILITY_SIZE
            ability_index
            opcode
            target
            power
            parameter1
            parameter2
            timing
            dispel_resistance
            duration
            probability1
            probability2
            max_level
            min_level
        STR_VAR resource
        END
END


DEFINE_PATCH_FUNCTION PATCH_SPL_EFFECTS
    STR_VAR patches_ref = 0
BEGIN
    LPF PATCH_ITM_SPL_EFFECTS
        INT_VAR ability_size = SPL_ABILITY_SIZE
        STR_VAR patches_ref
        END
END
