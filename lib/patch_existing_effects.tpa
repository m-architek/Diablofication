DEFINE_ACTION_FUNCTION PATCH_EXISTING_EFFECTS 
    STR_VAR patches_ref = 0
BEGIN 
    PRINT ~~
    PRINT ~Patching existing effects. This can take a while...~
    PRINT ~~

    LAF PATCH_EFF_V2_EFFECT_RECURSIVE STR_VAR glob = ~^.+\.eff$~ patches_ref
        RET_ARRAY ~%patches_ref%_box~
        END
    OUTER_SPRINT UNBOX_ARRAY ~%patches_ref%~
    LAM UNBOX_ARRAY

    PRINT ~~
    LAF COUNT_EXISTING STR_VAR glob = ~^.+\.spl$~ RET count END
    OUTER_SET counter = 0
    COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
        LPF PATCH_SPL_EFFECTS STR_VAR patches_ref END
        SET counter = counter + 1
        LPF PRINT_PROGRESS INT_VAR count counter END
        BUT_ONLY

    PRINT ~~
    LAF COUNT_EXISTING STR_VAR glob = ~^.+\.itm$~ RET count END
    OUTER_SET counter = 0
    COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
        LPF PATCH_ITM_EFFECTS STR_VAR patches_ref END
        SET counter = counter + 1
        LPF PRINT_PROGRESS INT_VAR count counter END
        BUT_ONLY

    LAF JOINABLE_NPC_ARRAY RET_ARRAY JOINABLE_NPC_ARRAY END
    ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS cre => _ BEGIN
        COPY_EXISTING ~%cre%~ ~override~
            LPF PATCH_CRE_EFFECTS STR_VAR patches_ref END
            BUT_ONLY
    END
END
