OUTER_SET LEVEL_DRAIN_MAX_LVL = 9

DEFINE_ACTION_MACRO BUILD_LEVEL_DRAIN_PATCHES BEGIN
    OUTER_FOR (drain_amount = 1; drain_amount <= LEVEL_DRAIN_MAX_LVL; ++drain_amount) BEGIN
        OUTER_SPRINT extension_resource_name ~%RESREF_SPL_LEVEL_DRAIN_PREFIX%%drain_amount%~
        
        ACTION_DEFINE_ASSOCIATIVE_ARRAY ~patch_LEVEL_DRAIN_%drain_amount%~ BEGIN
            match_opcode => EFF_OPCODE_LEVEL_DRAIN
            match_parameter1 => ~%drain_amount%~
            match_min_level => 0
            opcode => EFF_OPCODE_APPLY_EFFECTS_LIST
            parameter1 => 0
            parameter2 => 0
            resource => ~%extension_resource_name%~
        END

        ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN 
            ~LEVEL_DRAIN_%drain_amount%~ => ~patch_LEVEL_DRAIN_%drain_amount%~
        END
    END
END

DEFINE_ACTION_FUNCTION APPLY_LEVEL_DRAIN_EXTENSION BEGIN
    COPY ~%resources%/blank.spl~ ~override/%RESREF_SPL_LEVEL_DRAIN_KILL%.SPL~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_INSTANT_DEATH
            parameter1 = 1 // Show message
            parameter2 = 64 // Exploding stoned death/Level Drain death
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
            parameter1 = 1
            STR_VAR resource = ~%LOCALS_STATE_INSTANT_DEATH%~
            END

    OUTER_FOR (drain_amount = 1; drain_amount <= LEVEL_DRAIN_MAX_LVL; ++drain_amount) BEGIN
        OUTER_SPRINT extension_resource_name ~%RESREF_SPL_LEVEL_DRAIN_PREFIX%%drain_amount%~

        COPY ~%resources%/blank.spl~ ~override/%extension_resource_name%.SPL~
            LPF ADD_SPL_ABILITY RET ability_index END
            LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
                opcode = EFF_OPCODE_APPLY_EFFECTS_LIST
                max_level = drain_amount
                STR_VAR resource = ~%RESREF_SPL_LEVEL_DRAIN_KILL%~
                END
            LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
                opcode = EFF_OPCODE_LEVEL_DRAIN
                parameter1 = drain_amount
                min_level = drain_amount + 1
                END
    END
END
