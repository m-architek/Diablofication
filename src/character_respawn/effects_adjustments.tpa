INCLUDE ~%src%/effects_adjustments/petrification.tpa~
INCLUDE ~%src%/effects_adjustments/harm_damage.tpa~
INCLUDE ~%src%/effects_adjustments/disintegrate.tpa~
INCLUDE ~%src%/effects_adjustments/kill_creature.tpa~
INCLUDE ~%src%/effects_adjustments/kill_60hp.tpa~
INCLUDE ~%src%/effects_adjustments/level_drain.tpa~

DEFINE_ACTION_FUNCTION APPLY_EFFECTS_ADJUSTMENTS
    RET instant_death_variable patrification_state_variable
BEGIN
    OUTER_SPRINT instant_death_variable ~%LOCALS_STATE_INSTANT_DEATH%~
    OUTER_SPRINT patrification_state_variable ~%LOCALS_STATE_PETRIFICATION%~

    ACTION_TIME "cr_effects_adjustments" BEGIN
        LAF APPLY_EXISTING_EFFECTS_ADJUSTMENTS END
    END
    LAF APPLY_EFFECTS_EXTENSIONS END

    LAF APPLY_SAREVOK_STRIKE_ADJUSTMENT END
    LAF APPLY_TANARI_DEATH_GAZE_ADJUSTMENT END
    LAF APPLY_MIND_FLAYER_INT_DRAIN_ADJUSTMENT END
END

DEFINE_ACTION_FUNCTION APPLY_EXISTING_EFFECTS_ADJUSTMENTS BEGIN
    PRINT ~~
    PRINT ~Adjusting existing effects. This can take a while...~
    PRINT ~~

    LAF BUILD_PETRIFICATION_ADJUSTMENTS
        RET_ARRAY patch_PETRIFICATION patch_PETRIFICATION_IMMUNITY
        END

    LAF BUILD_HARM_DAMAGE_ADJUSTMENTS
        RET_ARRAY patch_HARM_DMG
        END

    LAF BUILD_DISINTEGRATE_ADJUSTMENTS
        RET_ARRAY patch_DISINTEGRATE patch_DISINTEGRATE_IMMUNITY
        END

    LAF BUILD_KILL_CREATURE_ADJUSTMENTS
        RET_ARRAY patch_KILL_CREATURE patch_KILL_CREATURE_IMMUNITY
        END

    LAF BUILD_KILL_60HP_ADJUSTMENTS
        RET_ARRAY patch_KILL_60HP patch_KILL_60HP_IMMUNITY
        END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN 
        PETRIFICATION => patch_PETRIFICATION 
        PETRIFICATION_IMMUNITY => patch_PETRIFICATION_IMMUNITY
        HARM_DMG => patch_HARM_DMG
        DISINTEGRATE => patch_DISINTEGRATE
        DISINTEGRATE_IMMUNITY => patch_DISINTEGRATE_IMMUNITY
        KILL_CREATURE => patch_KILL_CREATURE
        KILL_CREATURE_IMMUNITY => patch_KILL_CREATURE_IMMUNITY
        KILL_60HP => patch_KILL_60HP
        KILL_60HP_IMMUNITY => patch_KILL_60HP_IMMUNITY
    END

    LAM BUILD_LEVEL_DRAIN_PATCHES

    LAF PATCH_EFF_V2_EFFECT_RECURSIVE STR_VAR patches_ref = patches
        RET_ARRAY created_eff_resources
        END

    ACTION_PHP_EACH created_eff_resources AS key => value BEGIN 
        OUTER_SPRINT source_resource ~%key_0%~
        OUTER_SPRINT target_resource ~%key_1%~
        OUTER_SPRINT patch_name ~%source_resource%_%target_resource%~
        OUTER_SPRINT patch_ref ~patch_%patch_name%~

        ACTION_DEFINE_ASSOCIATIVE_ARRAY ~%patch_ref%~ BEGIN 
            patch_type => ~CLONE~
            match_resource => ~%source_resource%~
            resource => ~%target_resource%~
        END
        ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN 
            ~%patch_name%~ => ~%patch_ref%~ 
        END
    END

    ACTION_PHP_EACH patches AS key => value BEGIN 
        PRINT ~ Patch -> %key%~
    END
    
    COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
        LPF PATCH_SPL_EFFECTS STR_VAR patches_ref = patches END
        BUT_ONLY

    COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
        LPF PATCH_ITM_EFFECTS STR_VAR patches_ref = patches END
        BUT_ONLY
END

DEFINE_ACTION_FUNCTION APPLY_EFFECTS_EXTENSIONS BEGIN
    LAF APPLY_PETRIFICATION_EXTENSION END
    LAF APPLY_DISINTEGRATE_EXTENSION END
    LAF APPLY_KILL_CREATURE_EXTENSION END
    LAF APPLY_KILL_60HP_EXTENSION END
    LAF APPLY_LEVEL_DRAIN_EXTENSION END
END



DEFINE_ACTION_FUNCTION APPLY_SAREVOK_STRIKE_ADJUSTMENT BEGIN
    COPY_EXISTING ~SPWI979.SPL~ ~override~
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_GRAPHICS_REMOVE_SELECTION_CIRCLE END
        BUT_ONLY
END

DEFINE_ACTION_FUNCTION APPLY_TANARI_DEATH_GAZE_ADJUSTMENT BEGIN
    COPY_EXISTING ~SPIN996.SPL~ ~override~
        LPF CLONE_EFFECT INT_VAR match_opcode = EFF_OPCODE_SUMMON_REPLACE_CREATURE 
            opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
            parameter1 = 1
            parameter2 = 0
            STR_VAR resource = ~%LOCALS_STATE_INSTANT_DEATH%~
            END
        BUT_ONLY
END

DEFINE_ACTION_FUNCTION APPLY_MIND_FLAYER_INT_DRAIN_ADJUSTMENT BEGIN
    COPY_EXISTING ~MINDFLAY.ITM~ ~override~
        LPF CLONE_EFFECT INT_VAR match_opcode = EFF_OPCODE_INT_MOD 
            opcode = EFF_OPCODE_APPLY_EFFECTS_LIST
            parameter1 = 6
            parameter2 = 129 // 129_STAT(INT)<n
            STR_VAR resource = ~%RESREF_SPL_CR_INT_DRAIN_KILL%~
            END
        BUT_ONLY
    COPY ~%resources%/blank.spl~ ~override/%RESREF_SPL_CR_INT_DRAIN_KILL%.SPL~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
            parameter1 = 1
            STR_VAR resource = ~%LOCALS_STATE_INSTANT_DEATH%~
            END
END
