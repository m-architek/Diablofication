INCLUDE ~%src%/effects_adjustments/petrification.tpa~
INCLUDE ~%src%/effects_adjustments/harm_damage.tpa~
INCLUDE ~%src%/effects_adjustments/disintegrate.tpa~
INCLUDE ~%src%/effects_adjustments/kill_creature.tpa~
INCLUDE ~%src%/effects_adjustments/kill_60hp.tpa~
INCLUDE ~%src%/effects_adjustments/level_drain.tpa~
INCLUDE ~%src%/effects_adjustments/stat_drain.tpa~

DEFINE_ACTION_FUNCTION APPLY_EFFECTS_ADJUSTMENTS
    RET instant_death_variable patrification_state_variable
BEGIN
    PRINT ~~
    PRINT ~Adjusting existing effects. This can take a while...~
    PRINT ~~

    ACTION_CLEAR_ARRAY patches

    LAF APPLY_PETRIFICATION_PATCHES
        STR_VAR patches_ref = patches
        RET_ARRAY patches patch_PETRIFICATION patch_PETRIFICATION_IMMUNITY
        END

    LAF APPLY_HARM_DAMAGE_PATCHES
        STR_VAR patches_ref = patches
        RET_ARRAY patches patch_HARM_DMG
        END

    LAF APPLY_DISINTEGRATE_PATCHES
        STR_VAR patches_ref = patches
        RET_ARRAY patches patch_DISINTEGRATE patch_DISINTEGRATE_IMMUNITY
        END

    LAF APPLY_KILL_CREATURE_PATCHES
        STR_VAR patches_ref = patches
        RET_ARRAY patches patch_KILL_CREATURE patch_KILL_CREATURE_IMMUNITY
        END

    LAF APPLY_KILL_60HP_PATCHES
        STR_VAR patches_ref = patches
        RET_ARRAY patches patch_KILL_60HP patch_KILL_60HP_IMMUNITY
        END

    LAF APPLY_LEVEL_DRAIN_PATCHES 
        STR_VAR patches_ref = patches
        RET_ARRAY patches patch_LEVEL_DRAIN_KILL patch_LEVEL_DRAIN patch_LEVEL_DRAIN_IMMUNITY
        END

    LAF APPLY_STAT_DRAIN_PATCHES 
        STR_VAR patches_ref = patches
        RET_ARRAY patches patch_INT_DRAIN
        END

    LAF PATCH_ALL_EFFECTS STR_VAR patches_ref = patches END


    OUTER_SPRINT instant_death_variable ~%LOCALS_STATE_INSTANT_DEATH%~
    OUTER_SPRINT patrification_state_variable ~%LOCALS_STATE_PETRIFICATION%~

    LAF APPLY_PETRIFICATION_RESOURCES STR_VAR patrification_state_variable END
    LAF APPLY_DISINTEGRATE_RESOURCES STR_VAR instant_death_variable END
    LAF APPLY_KILL_CREATURE_RESOURCES STR_VAR instant_death_variable END
    LAF APPLY_KILL_60HP_RESOURCES STR_VAR instant_death_variable END
    LAF APPLY_LEVEL_DRAIN_RESOURCES STR_VAR instant_death_variable END
    LAF APPLY_STAT_DRAIN_RESOURCES STR_VAR instant_death_variable END

    LAF APPLY_SAREVOK_STRIKE_ADJUSTMENT END
    LAF APPLY_TANARI_DEATH_GAZE_ADJUSTMENT STR_VAR instant_death_variable END
END

DEFINE_ACTION_FUNCTION APPLY_SAREVOK_STRIKE_ADJUSTMENT BEGIN
    COPY_EXISTING ~SPWI979.SPL~ ~override~
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_GRAPHICS_REMOVE_SELECTION_CIRCLE END
        BUT_ONLY
END

DEFINE_ACTION_FUNCTION APPLY_TANARI_DEATH_GAZE_ADJUSTMENT 
    STR_VAR instant_death_variable = 0
BEGIN
    COPY_EXISTING ~SPIN996.SPL~ ~override~
        LPF CLONE_EFFECT INT_VAR match_opcode = EFF_OPCODE_SUMMON_REPLACE_CREATURE 
            opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
            parameter1 = 1
            parameter2 = 0
            STR_VAR resource = ~%instant_death_variable%~
            END
        BUT_ONLY
END
