DEFINE_ACTION_FUNCTION APPLY_RESPAWN_TRIGGER
    STR_VAR respawn_execution_cutscene = 0
        patrification_state_variable = 0
        instant_death_variable = 0
        kill_player1_variable = 0
        maze_state_variable = 0
BEGIN
    <<<<<<<< .../respawn_trigger.baf
    IF
        Global("%kill_player1_variable%", "GLOBAL", 1)
        GlobalTimerExpired("PoisonParty2", "GLOBAL")
        Global("PartyCured", "GLOBAL", 0)
    THEN
        RESPONSE #100
            SetGlobalTimer("PoisonParty", "GLOBAL", ONE_DAY)
            SetGlobalTimer("PoisonParty2", "GLOBAL", TEN_DAYS)
            SetGlobal("PoisonWarning" ,"GLOBAL", 0)
            Continue()
    END

    IF
        OR(3)
            HP(Player1, 1)
            Global("%kill_player1_variable%", "GLOBAL", 1)
            TriggerOverride(Player1, Global("%instant_death_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            ActionOverride(Player1, SetSequence(SEQ_SLEEP))
            ActionOverride(Player1, VerbalConstant(Player1, DYING))
            ApplySpellRES("%RESREF_SPL_CR_VISUAL_DEATH%", Player1)
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END

    IF
        %Player1_unavailable%
        %Player2_unavailable%
        %Player3_unavailable%
        %Player4_unavailable%
        %Player5_unavailable%
        %Player6_unavailable%
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            ActionOverride(Player1, SetSequence(SEQ_SLEEP))
            ActionOverride(Player1, VerbalConstant(Player1, DYING))
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END

    IF
        TriggerOverride(Player1, Global("%patrification_state_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            ActionOverride(Player1, SetSequence(SEQ_SLEEP))
            ApplySpellRES("%RESREF_SPL_CR_VISUAL_PETRIFICATION%", Player1)
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)            
    END
    >>>>>>>>
    

    COPY ~%resources%/blank.spl~ ~override/%RESREF_SPL_CR_VISUAL_DEATH%.SPL~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = 25282 // Death
            END                        
        BUT_ONLY
    
    COPY ~%resources%/blank.spl~ ~override/%RESREF_SPL_CR_VISUAL_PETRIFICATION%.SPL~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = 25863 // Petrified
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_COLOUR_BY_PALETTE
            parameter1 = 72 // Stone Dead Grey
            parameter2 = 255 // Whole Body
            dispel_resistance = EFF_DISPELLABLE_UNBLOCKABLE
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_PLAY_SOUND
            STR_VAR resource = ~MISC_06B~
            END            
        BUT_ONLY

    ACTION_CLEAR_ARRAY players
    ACTION_DEFINE_ARRAY players BEGIN Player1 Player2 Player3 Player4 Player5 Player6 END

    EXTEND_BOTTOM ~baldur.bcs~ ~.../respawn_trigger.baf~ 
        PHP_EACH players AS _ => player BEGIN

            // Note TriggerOverride + OR bug
            // https://www.gibberlings3.net/forums/topic/31891-nexttriggerobject-documentation-not-accurate/#comment-287192
            SPRINT ~%player%_unavailable~ ~~~~~
                OR(5)
                    !Exists(%player%)
                    StateCheck(%player%, STATE_REALLY_DEAD)
                    StateCheck(%player%, STATE_CHARMED)
                    TriggerOverride(%player%, Global("%maze_state_variable%", "LOCALS", 1))
                    TriggerOverride(Player1, False())
            ~~~~~
        END
        EVALUATE_BUFFER
END
