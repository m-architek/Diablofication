DEFINE_ACTION_FUNCTION APPLY_RESPAWN_TRIGGER
    STR_VAR respawn_execution_cutscene = 0
        patrification_state_variable = 0
        instant_death_variable = 0
        kill_player1_variable = 0
        maze_state_variable = 0
BEGIN
    <<<<<<<< .../respawn_trigger.baf
    IF
        Global("%kill_player1_variable%", "GLOBAL", 1)
        GlobalTimerExpired("PoisonParty2", "GLOBAL")
        Global("PartyCured", "GLOBAL", 0)
    THEN
        RESPONSE #100
            SetGlobalTimer("PoisonParty", "GLOBAL", ONE_DAY)
            SetGlobalTimer("PoisonParty2", "GLOBAL", TEN_DAYS)
            SetGlobal("PoisonWarning" ,"GLOBAL", 0)
            Continue()
    END

    IF
        OR(3)    
            HP(Player1, 1)
            Global("%kill_player1_variable%", "GLOBAL", 1)
            TriggerOverride(Player1, Global("%instant_death_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            %Player1_death_animation%
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END


    IF
        %OR_Player1_unavailable%
        %OR_Player2_unavailable%
        %OR_Player3_unavailable%
        %OR_Player4_unavailable%
        %OR_Player5_unavailable%
        %OR_Player6_unavailable%
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            %Player1_death_animation%
            %Player2_death_animation%
            %Player3_death_animation%
            %Player4_death_animation%
            %Player5_death_animation%
            %Player6_death_animation%
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END

    IF
        TriggerOverride(Player1, Global("%patrification_state_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            CreateVisualEffectObject("%VVC_RESREF_FLESH_TO_STONE%", Player1)
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END
    >>>>>>>>

    ACTION_DEFINE_ARRAY players BEGIN Player1 Player2 Player3 Player4 Player5 Player6 END

    EXTEND_BOTTOM ~baldur.bcs~ ~.../respawn_trigger.baf~ 
        PHP_EACH players AS _ => player BEGIN
            SPRINT ~%player%_death_animation~ ~~~~~
                ActionOverride(%player%, SetSequence(SEQ_SLEEP))
                ActionOverride(%player%, VerbalConstant(%player%, DYING))
            ~~~~~

            // Note TriggerOverride + OR bug
            // https://www.gibberlings3.net/forums/topic/31891-nexttriggerobject-documentation-not-accurate/#comment-287192
            SPRINT ~OR_%player%_unavailable~ ~~~~~
                OR(5)
                    !Exists(%player%)
                    StateCheck(%player%, STATE_REALLY_DEAD)
                    StateCheck(%player%, STATE_CHARMED)
                    TriggerOverride(%player%, Global("%maze_state_variable%", "LOCALS", 1))
                    TriggerOverride(Player1, False())
            ~~~~~
        END
        EVALUATE_BUFFER
END
