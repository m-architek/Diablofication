DEFINE_ACTION_FUNCTION APPLY_RESPAWN_TRIGGER
    STR_VAR respawn_cutscene = 0
            game_over_variable = 0
            game_over_cutscene = 0
            death_state_variable = 0
            petrification_state_variable  = 0
            imprisonment_state_variable = 0
            death_prevention_variable = 0
BEGIN

    <<<<<<<< .../player1_eliminated.baf
    IF
        Global("%game_over_variable%", "GLOBAL", 0)
        OR(4)
            StateCheck(Player1, STATE_REALLY_DEAD)
            TriggerOverride(Player1, Global("%death_state_variable%", "LOCALS", 1))
            TriggerOverride(Player1, Global("%petrification_state_variable%", "LOCALS", 1))
            TriggerOverride(Player1, Global("%imprisonment_state_variable%", "LOCALS", 1))
        OR(3)
            !Exists(Player2)
            TriggerOverride(Player2, Global("%death_prevention_variable%", "LOCALS", 0))
            TriggerOverride(Player1, False())
    THEN
        RESPONSE #100
            SetGlobal("%game_over_variable%", "GLOBAL", 1)
    END
    >>>>>>>>

    LAF EXTEND_GLOBAL_SCRIPT STR_VAR baf = ~.../player1_eliminated.baf~  END


     
    DEFINE_PATCH_FUNCTION REPLACE_GAME_OVER_WITH_RESPAWN BEGIN 
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH 
            ~StartCutSceneEx("%game_over_cutscene%",TRUE)~ 
            ~StartCutSceneEx("%respawn_cutscene%",TRUE)~
    END
    LAF PATCH_GLOBAL_SCRIPT STR_VAR patch_fn = ~REPLACE_GAME_OVER_WITH_RESPAWN~ END
END
