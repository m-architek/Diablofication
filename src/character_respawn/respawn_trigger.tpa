DEFINE_ACTION_FUNCTION APPLY_RESPAWN_TRIGGER
    INT_VAR is_party_revive = ~~
    STR_VAR respawn_cutscene = 0
            game_over_variable = 0
            respawn_trigger_variable = 0
            death_state_variable = 0
            petrification_state_variable  = 0
            imprisonment_state_variable = 0
BEGIN
    ACTION_IF (NOT is_party_revive) BEGIN 

        <<<<<<<< .../player1_eliminated.baf
        IF
            OR(4)
                StateCheck(Player1, STATE_REALLY_DEAD)
                TriggerOverride(Player1, Global("%death_state_variable%", "LOCALS", 1))
                TriggerOverride(Player1, Global("%petrification_state_variable%", "LOCALS", 1))
                TriggerOverride(Player1, Global("%imprisonment_state_variable%", "LOCALS", 1))
        THEN
            RESPONSE #100
                SetGlobal("%respawn_trigger_variable%", "GLOBAL", 1)
                Continue()
        END
        >>>>>>>>

        EXTEND_BOTTOM ~baldur.bcs~ ~.../player1_eliminated.baf~ 
            EVALUATE_BUFFER
    END


    COPY_EXISTING ~baldur.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN 
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH 
                ~SetGlobal("%game_over_variable%","GLOBAL",1)~ 
                ~SetGlobal("%respawn_trigger_variable%", "GLOBAL", 1)~
        END
        BUT_ONLY


    <<<<<<<< .../respawn_trigger.baf
    IF
        Global("%respawn_trigger_variable%", "GLOBAL", 1)
        GlobalTimerExpired("PoisonParty2", "GLOBAL")
        Global("PartyCured", "GLOBAL", 0)
    THEN
        RESPONSE #100
            SetGlobalTimer("PoisonParty", "GLOBAL", ONE_DAY)
            SetGlobalTimer("PoisonParty2", "GLOBAL", TEN_DAYS)
            SetGlobal("PoisonWarning" ,"GLOBAL", 0)
            Continue()
    END

    IF
        Global("%respawn_trigger_variable%", "GLOBAL", 1)
    THEN
        RESPONSE #100
            SetGlobal("%respawn_trigger_variable%", "GLOBAL", 0)
            ClearAllActions()
            StartCutSceneMode()
            StartCutSceneEx("%respawn_cutscene%", TRUE)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~baldur.bcs~ ~.../respawn_trigger.baf~ 
        EVALUATE_BUFFER
END
