DEFINE_ACTION_FUNCTION APPLY_RESPAWN_TRIGGER
    STR_VAR respawn_execution_cutscene = 0
        patrification_state_variable = 0
        kill_player1_variable = 0
BEGIN
    <<<<<<<< .../respawn_trigger.baf
    IF
        OR(2)    
            HP(Player1, 1)
            Global("%kill_player1_variable%", "GLOBAL", 1)
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            ActionOverride(Player1, SetSequence(SEQ_SLEEP))
            ActionOverride(Player1, VerbalConstant(Player1, DYING))
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END

    IF
        StateCheck(Player1, STATE_CHARMED)
        OR(3)
            !Exists(Player2)
            StateCheck(Player2, STATE_REALLY_DEAD)
            StateCheck(Player2, STATE_CHARMED)
        OR(3)
            !Exists(Player3)
            StateCheck(Player3, STATE_REALLY_DEAD)
            StateCheck(Player3, STATE_CHARMED)
        OR(3)
            !Exists(Player4)
            StateCheck(Player4, STATE_REALLY_DEAD)
            StateCheck(Player4, STATE_CHARMED)
        OR(3)
            !Exists(Player5)
            StateCheck(Player5, STATE_REALLY_DEAD)
            StateCheck(Player5, STATE_CHARMED)
        OR(3)
            !Exists(Player6)
            StateCheck(Player6, STATE_REALLY_DEAD)
            StateCheck(Player6, STATE_CHARMED)
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END

    IF
        TriggerOverride(Player1, Global("%patrification_state_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            CreateVisualEffectObject("%VVC_RESREF_FLESH_TO_STONE%", Player1)
            StartCutSceneEx("%respawn_execution_cutscene%", TRUE)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~baldur.bcs~ ~.../respawn_trigger.baf~ 
        EVALUATE_BUFFER
END
