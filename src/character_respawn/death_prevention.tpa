DEFINE_ACTION_FUNCTION APPLY_DEATH_PREVENTION BEGIN
    LAF APPLY_PLAYER1_CAN_DIE_FLAG END
    LAF APPLY_DEATH_PREVENTION_SPL END
END


DEFINE_ACTION_FUNCTION APPLY_PLAYER1_CAN_DIE_FLAG BEGIN
    COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
        WRITE_LONG 0x14 (THIS | BIT4)
        BUT_ONLY
END


DEFINE_ACTION_FUNCTION APPLY_DEATH_PREVENTION_SPL BEGIN

    COPY ~%resources%/blank.spl~ ~override/%RESREF_SPL_CR_DEATH_PREVENTION%.SPL~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_HP_MINIMUM
            parameter1 = 1
            timing = EFF_TIMING_INSTANT_PERMANENT
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_HP_MAXIMUM
            parameter1 = 1
            timing = EFF_TIMING_INSTANT_PERMANENT
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_PROTECTION_FROM_OPCODE
            parameter2 = EFF_OPCODE_STATE_PETRIFICATION
            timing = EFF_TIMING_INSTANT_PERMANENT
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_MINIMUM_BASE_STATS
            parameter2 = 1
            timing = EFF_TIMING_INSTANT_PERMANENT
            END

    <<<<<<<< .../enable_death_prevention.baf
    IF
        OnCreation()
        Global("%GLOBAL_CR_DEATH_PREVENTION_ON%", "GLOBAL", 0)
    THEN
        RESPONSE #100
            ApplySpellRES("%RESREF_SPL_CR_DEATH_PREVENTION%", Player1)
            SetGlobal("%GLOBAL_CR_DEATH_PREVENTION_ON%", "GLOBAL", 1)
            Continue()
    END
    >>>>>>>>
    
    EXTEND_TOP ~baldur.bcs~ ~.../enable_death_prevention.baf~
        EVALUATE_BUFFER
END
