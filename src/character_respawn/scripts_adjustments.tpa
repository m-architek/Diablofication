DEFINE_ACTION_FUNCTION APPLY_SCRIPTS_ADJUSTMENTS 
    RET kill_player1_variable
BEGIN
    OUTER_SPRINT kill_player1_variable ~%GLOBAL_CR_KILL_PLAYER1%~

    COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_KILL_PLAYER1_ACTIONS STR_VAR kill_player1_variable END
            LPF PATCH_1HP_TRIGGERS_AND_ACTIONS END
        END
        BUT_ONLY

    COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_KILL_PLAYER1_ACTIONS STR_VAR kill_player1_variable END
            LPF PATCH_1HP_TRIGGERS_AND_ACTIONS END
        END
        BUT_ONLY

    LAF APPLY_PALACE_ESCORT_MISSION_ADJUSTMENTS END
END

DEFINE_PATCH_FUNCTION PATCH_KILL_PLAYER1_ACTIONS 
    STR_VAR kill_player1_variable = 0
BEGIN
    SPRINT kill_player1_action ~~~~~
        EndCutSceneMode()
        SetGlobal("%kill_player1_variable%", "GLOBAL", 1)
        Continue()
    ~~~~~

    REPLACE_TEXTUALLY EXACT_MATCH ~Kill(Player1)~ ~%kill_player1_action%~
    REPLACE_TEXTUALLY EXACT_MATCH ~Kill(Protagonist)~ ~%kill_player1_action%~
    REPLACE_TEXTUALLY EXACT_MATCH ~GameOver(19377)~ ~%kill_player1_action%~
END

DEFINE_PATCH_FUNCTION PATCH_1HP_TRIGGERS_AND_ACTIONS BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH ~HP(Player1,1) ~ ~HP(Player1,2)~
    REPLACE_TEXTUALLY EXACT_MATCH ~HP(Protagonist,1) ~ ~HP(Protagonist,2)~
    REPLACE_TEXTUALLY EXACT_MATCH ~HPLT(Player1,2) ~ ~HPLT(Player1,3)~
    REPLACE_TEXTUALLY EXACT_MATCH ~HPLT(Protagonist,2) ~ ~HPLT(Protagonist,3)~
    REPLACE_TEXTUALLY EXACT_MATCH ~ChangeStat(Player1,CURHITPOINTS,1,SET) ~ ~ChangeStat(Player1,CURHITPOINTS,2,SET)~
    REPLACE_TEXTUALLY EXACT_MATCH ~ChangeStat(Protagonist,CURHITPOINTS,1,SET) ~ ~ChangeStat(Protagonist,CURHITPOINTS,2,SET)~
END

DEFINE_ACTION_FUNCTION APPLY_PALACE_ESCORT_MISSION_ADJUSTMENTS BEGIN 

    COPY_EXISTING ~BELT.CRE~ ~override~
        ADD_CRE_ITEM ~MINHP1~ #0 #0 #0 ~NONE~ ~AMULET~
        BUT_ONLY

    COPY_EXISTING ~LIIA.CRE~ ~override~
        ADD_CRE_ITEM ~MINHP1~ #0 #0 #0 ~NONE~ ~AMULET~
        BUT_ONLY
    
    COPY_EXISTING ~SARDOPP.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY EXACT_MATCH ~AttackReevaluate("Liia"~ ~AttackReevaluate(Player1~
            REPLACE_TEXTUALLY EXACT_MATCH ~AttackReevaluate("Belt"~ ~AttackReevaluate([PC]~
            REPLACE_TEXTUALLY EXACT_MATCH ~Attack("Liia"~ ~Attack(Player1~
            REPLACE_TEXTUALLY EXACT_MATCH ~Attack("Belt"~ ~Attack([PC]~
        END
        BUT_ONLY

    COPY_EXISTING ~SAREVO.DLG~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY EXACT_MATCH ~SetGlobal("SarevokBehavior","GLOBAL",10)~ ~StartCutSceneMode()~
        END
        BUT_ONLY   
END
