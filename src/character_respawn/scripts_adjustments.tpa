DEFINE_ACTION_FUNCTION APPLY_SCRIPTS_ADJUSTMENTS 
    RET respawn_trigger_variable
BEGIN
    OUTER_SPRINT respawn_trigger_variable ~%GLOBAL_RESPAWN_TRIGGER%~

    COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_GAME_OVER_ACTIONS STR_VAR respawn_trigger_variable END
        END
        BUT_ONLY

    COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_GAME_OVER_ACTIONS STR_VAR respawn_trigger_variable END
        END
        BUT_ONLY

    LAF APPLY_PALACE_ESCORT_MISSION_ADJUSTMENTS END
END


DEFINE_PATCH_FUNCTION PATCH_GAME_OVER_ACTIONS 
    STR_VAR respawn_trigger_variable = 0
BEGIN
    SPRINT respawn_trigger_action ~~~~~
        EndCutSceneMode()
        SetGlobal("%respawn_trigger_variable%", "GLOBAL", 1)
        Continue()
    ~~~~~

    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~GameOver(19377)~ ~%respawn_trigger_action%~
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~GameOver(-1)~ ~%respawn_trigger_action%~
END


DEFINE_ACTION_FUNCTION APPLY_PALACE_ESCORT_MISSION_ADJUSTMENTS BEGIN 

    COPY_EXISTING ~BELT.CRE~ ~override~
        ADD_CRE_ITEM ~MINHP1~ #0 #0 #0 ~NONE~ ~AMULET~
        BUT_ONLY

    COPY_EXISTING ~LIIA.CRE~ ~override~
        ADD_CRE_ITEM ~MINHP1~ #0 #0 #0 ~NONE~ ~AMULET~
        BUT_ONLY
    
    COPY_EXISTING ~SARDOPP.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AttackReevaluate("Liia"~ ~AttackReevaluate(Player1~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AttackReevaluate("Belt"~ ~AttackReevaluate([PC]~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Attack("Liia"~ ~Attack(Player1~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Attack("Belt"~ ~Attack([PC]~
        END
        BUT_ONLY

    COPY_EXISTING ~SAREVO.DLG~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~SetGlobal("SarevokBehavior","GLOBAL",10)~ ~StartCutSceneMode()~
        END
        BUT_ONLY   
END
