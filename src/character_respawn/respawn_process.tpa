DEFINE_ACTION_FUNCTION APPLY_RESPAWN_PROCESS 
    STR_VAR penalty_cutscene = 0
            death_state_variable = 0
            petrification_state_variable  = 0
            imprisonment_state_variable = 0
            maze_state_variable = 0
            revive_death_spell = 0
            revive_petrification_spell = 0
            revive_imprisonment_spell = 0
    RET respawn_begin_cutscene
BEGIN
    OUTER_SPRINT respawn_begin_cutscene ~%RESREF_BCS_RESPAWN_BEGIN%~
    OUTER_SPRINT respawn_end_cutscene ~%RESREF_BCS_RESPAWN_END%~

    OUTER_SET portal_next_cutscene_index = 2

    LAF APPLY_RESPAWN_PORTAL INT_VAR next_cutscene_index = ~%portal_next_cutscene_index%~
        STR_VAR next_cutscene = ~%respawn_end_cutscene%~
        RET portal_cutscene
            portal_owner_variable
            portal_next_cutscene_variable = next_cutscene_variable
        END

    LAF APPLY_RESPAWN_BEGIN INT_VAR portal_next_cutscene_index
        STR_VAR death_state_variable
                petrification_state_variable 
                imprisonment_state_variable
                maze_state_variable
                revive_death_spell
                revive_petrification_spell
                revive_imprisonment_spell
                portal_owner_variable
                portal_next_cutscene_variable
                this_cutscene = ~%respawn_begin_cutscene%~
                next_cutscene = ~%portal_cutscene%~
        END

    LAF APPLY_RESPAWN_END
        STR_VAR this_cutscene = ~%respawn_end_cutscene%~
                next_cutscene = ~%penalty_cutscene%~
        END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_BEGIN 
    INT_VAR portal_next_cutscene_index = ~~
    STR_VAR death_state_variable = 0
            petrification_state_variable  = 0
            imprisonment_state_variable = 0
            maze_state_variable = 0
            revive_death_spell = 0
            revive_petrification_spell = 0
            revive_imprisonment_spell = 0
            portal_owner_variable = 0
            portal_next_cutscene_variable = 0
            this_cutscene = 0
            next_cutscene = 0
BEGIN 
    LAF APPLY_RESPAWN_CUTSCENE_PERTRIFICATION RET cuscene_petrification_spl END
    LAF APPLY_RESPAWN_RESURRECTION 
        STR_VAR revive_death_spell
                revive_petrification_spell
                revive_imprisonment_spell
                cuscene_petrification_spl
        RET resurrection_spl 
        END 
    
    <<<<<<<< .../respawn_begin_player_prepare.baf
    IF
        Exists(%player%)
        !StateCheck(%player%, STATE_REALLY_DEAD)
        TriggerOverride(%player%, Global("%petrification_state_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            CutSceneId(%player%)
            SetSequence(SEQ_READY)
            ApplySpellRES("%cuscene_petrification_spl%", %player%)
    END

    IF
        Exists(%player%)
        !StateCheck(%player%, STATE_REALLY_DEAD)
        TriggerOverride(%player%, Global("%maze_state_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            CutSceneId(%player%)
            SetSequence(SEQ_SLEEP)
            SetGlobal("%maze_state_variable%", "LOCALS", 0)
    END
    >>>>>>>>

    ACTION_PHP_EACH PLAYERS AS _ => player BEGIN 

        EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../respawn_begin_player_prepare.baf~ 
            EVALUATE_BUFFER
    END

    <<<<<<<< .../respawn_begin.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SmallWait(60)
            FadeToColor([30.0], 0)
            SmallWait(30)

            ApplySpellRES("%resurrection_spl%", Player1)
            ApplySpellRES("%resurrection_spl%", Player2)
            ApplySpellRES("%resurrection_spl%", Player3)
            ApplySpellRES("%resurrection_spl%", Player4)
            ApplySpellRES("%resurrection_spl%", Player5)
            ApplySpellRES("%resurrection_spl%", Player6)   

            SetGlobal("%portal_next_cutscene_variable%", "GLOBAL", %portal_next_cutscene_index%)
            SetGlobal("%portal_owner_variable%", "GLOBAL", 0)
            StartCutSceneEx("%next_cutscene%",TRUE)     
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../respawn_begin.baf~ 
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_END
    STR_VAR this_cutscene = 0
            next_cutscene = 0
BEGIN 
    LAF APPLY_RESPAWN_HEAL RET heal_spl END
    
    <<<<<<<< .../respawn_end.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SetSequence(SEQ_READY)
            ActionOverride(Player2, SetSequence(SEQ_READY))
            ActionOverride(Player3, SetSequence(SEQ_READY))
            ActionOverride(Player4, SetSequence(SEQ_READY))
            ActionOverride(Player5, SetSequence(SEQ_READY))
            ActionOverride(Player6, SetSequence(SEQ_READY))
            SmallWait(15)

            ApplySpellRES("%heal_spl%", Player1)
            ApplySpellRES("%heal_spl%", Player2)
            ApplySpellRES("%heal_spl%", Player3)
            ApplySpellRES("%heal_spl%", Player4)
            ApplySpellRES("%heal_spl%", Player5)
            ApplySpellRES("%heal_spl%", Player6)

            SmallWait(15)
            FadeFromColor([30.0], 0)
            SmallWait(30)
            StartCutSceneEx("%next_cutscene%", TRUE)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../respawn_end.baf~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_CUTSCENE_PERTRIFICATION 
    RET cuscene_petrification_spl
BEGIN
    OUTER_SPRINT cuscene_petrification_spl ~%RESREF_SPL_CUTSCENE_PETRIFICATION%~

    CREATE ~SPL~ ~%cuscene_petrification_spl%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SLOW
            timing = EFF_TIMING_INSTANT_PERMANENT // Allows for removal
            END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_RESURRECTION 
    STR_VAR revive_death_spell = 0
            revive_petrification_spell = 0
            revive_imprisonment_spell = 0
            cuscene_petrification_spl = 0
    RET resurrection_spl
BEGIN
    OUTER_SPRINT resurrection_spl ~%RESREF_SPL_RESURRECTION%~

    CREATE ~SPL~ ~%resurrection_spl%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_PROTECTION_FROM_OPCODE
            parameter2 = EFF_OPCODE_PLAY_SOUND
            timing = EFF_TIMING_INSTANT_LIMITED
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_EFFECTS_BY_RESOURCE
            STR_VAR resource = ~%cuscene_petrification_spl%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CAST_SPELL
            parameter1 = 0 // Casting Level
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%revive_death_spell%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CAST_SPELL
            parameter1 = 0 // Casting Level
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%revive_petrification_spell%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CAST_SPELL
            parameter1 = 0 // Casting Level
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%revive_imprisonment_spell%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_PROTECTION_FROM_RESOURCE
            parameter1 = 3 // DEAD
            parameter2 = 113 // GENERAL!=n
            timing = EFF_TIMING_INSTANT_LIMITED
            STR_VAR resource = ~%resurrection_spl%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_RAISE_DEAD
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CAST_SPELL
            parameter1 = 0 // Casting Level
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~#RDREMOV~
            END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_HEAL 
    RET heal_spl
BEGIN
    LAF SPELL_IDS_RESOURCE_OF_SYMBOL STR_VAR ids_name = CLERIC_HEAL
        RET heal_resref = spell_resource
        END
    LAF SPELL_IDS_RESOURCE_OF_SYMBOL STR_VAR ids_name = CLERIC_NEUTRALIZE_POISON
        RET cure_resref = spell_resource
        END
    LAF SPELL_IDS_RESOURCE_OF_SYMBOL STR_VAR ids_name = CLERIC_RESTORATION
        RET restoration_resref = spell_resource
        END
    LAF SPELL_IDS_RESOURCE_OF_SYMBOL STR_VAR ids_name = CLERIC_SPIRITUAL_CLARITY
        RET clarity_resref = spell_resource
        END

    COPY_EXISTING ~%heal_resref%.SPL~ ~override/%RESREF_SPL_HEAL_HEAL%.SPL~ 
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        BUT_ONLY
    COPY_EXISTING ~%cure_resref%.SPL~ ~override/%RESREF_SPL_HEAL_CURE%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        BUT_ONLY
    COPY_EXISTING ~%restoration_resref%.SPL~ ~override/%RESREF_SPL_HEAL_RESTORATION%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_FATIGUE_MODIFIER END
        BUT_ONLY
    COPY_EXISTING ~%clarity_resref%.SPL~ ~override/%RESREF_SPL_HEAL_CLARITY%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        BUT_ONLY

    OUTER_SPRINT heal_spl ~%RESREF_SPL_HEAL%~

    CREATE ~SPL~ ~%heal_spl%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_ALTERNATIVE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_HEAL%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_CURE%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_RESTORATION%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_CLARITY%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPEL_MAGIC
            parameter1 = 0 // Level
            parameter2 = 0 // Always dispel
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = 25820 // Dispel Effects
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_STAT_MORALE
            parameter1 = 10
            parameter2 = 1 // Set to value
            special = 1 // Use parameters
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SPELL_MAGICAL_REST
            END 
END
