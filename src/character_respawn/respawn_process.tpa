DEFINE_ACTION_FUNCTION APPLY_RESPAWN_PROCESS 
    STR_VAR penalty_cutscene_name = 0
        patrification_state_variable = 0
        instant_death_variable = 0
        kill_player1_variable = 0
        maze_state_variable = 0
    RET respawn_cutscene_name
BEGIN
    OUTER_SET teleport_next_cutscene_variable_value = 2

    LAF APPLY_RESPAWN_HEAL_CUTSCENE STR_VAR next_cutscene_name = ~%penalty_cutscene_name%~
        RET respawn_heal_cutscene_name 
        END

    LAF APPLY_RESPAWN_TELEPORT INT_VAR teleport_next_cutscene_variable_value
        STR_VAR teleport_next_cutscene_name = ~%respawn_heal_cutscene_name%~
        RET teleport_cutscene_name
            teleport_next_cutscene_variable_name
        END

    LAF APPLY_RESPAWN_RESURRECTION_CUTSCENE INT_VAR teleport_next_cutscene_variable_value
        STR_VAR teleport_cutscene_name
            patrification_state_variable = ~%patrification_state_variable%~
            instant_death_variable = ~%instant_death_variable%~
            kill_player1_variable = ~%kill_player1_variable%~
            maze_state_variable = ~%maze_state_variable%~
            teleport_next_cutscene_variable_name
        RET respawn_cutscene_name = respawn_resurrection_cutscene_name 
        END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_RESURRECTION_CUTSCENE 
    INT_VAR teleport_next_cutscene_variable_value = ~~
    STR_VAR teleport_cutscene_name = 0
        patrification_state_variable = 0
        instant_death_variable = 0
        kill_player1_variable = 0
        maze_state_variable = 0
        teleport_next_cutscene_variable_name = 0
    RET respawn_resurrection_cutscene_name
BEGIN 
    OUTER_SPRINT respawn_resurrection_cutscene_name ~%RESREF_BCS_RESPAWN_RESURRECTION_CUTSCENE%~

    LAF APPLY_RESPAWN_RESURRECTION RET resurrection_spl END
    
    <<<<<<<< .../respawn_resurrection_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SmallWait(60)
            FadeToColor([30.0], 0)
            SmallWait(30)

            ApplySpellRES("%resurrection_spl%", Player1)
            ApplySpellRES("%resurrection_spl%", Player2)
            ApplySpellRES("%resurrection_spl%", Player3)
            ApplySpellRES("%resurrection_spl%", Player4)
            ApplySpellRES("%resurrection_spl%", Player5)
            ApplySpellRES("%resurrection_spl%", Player6)

            SetGlobal("%kill_player1_variable%", "GLOBAL", 0)
            SetGlobal("%patrification_state_variable%", "LOCALS", 0)
            SetGlobal("%maze_state_variable%", "LOCALS", 0)
            SetGlobal("%instant_death_variable%", "LOCALS", 0)

            SetSequence(SEQ_READY)
            ActionOverride(Player2, SetSequence(SEQ_READY))
            ActionOverride(Player3, SetSequence(SEQ_READY))
            ActionOverride(Player4, SetSequence(SEQ_READY))
            ActionOverride(Player5, SetSequence(SEQ_READY))
            ActionOverride(Player6, SetSequence(SEQ_READY)) 

            SetGlobal("%teleport_next_cutscene_variable_name%", "GLOBAL", %teleport_next_cutscene_variable_value%)
            StartCutSceneEx("%teleport_cutscene_name%",TRUE)      
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%respawn_resurrection_cutscene_name%.BCS~ ~.../respawn_resurrection_cutscene.baf~ 
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_HEAL_CUTSCENE 
    STR_VAR next_cutscene_name = 0
    RET respawn_heal_cutscene_name
BEGIN 
    OUTER_SPRINT respawn_heal_cutscene_name ~%RESREF_BCS_RESPAWN_HEAL_CUTSCENE%~

    LAF APPLY_RESPAWN_HEAL RET heal_spl END
    
    <<<<<<<< .../respawn_heal_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SmallWait(15)

            ApplySpellRES("%heal_spl%", Player1)
            ApplySpellRES("%heal_spl%", Player2)
            ApplySpellRES("%heal_spl%", Player3)
            ApplySpellRES("%heal_spl%", Player4)
            ApplySpellRES("%heal_spl%", Player5)
            ApplySpellRES("%heal_spl%", Player6)

            SmallWait(15)
            FadeFromColor([30.0], 0)
            SmallWait(30)
            StartCutSceneEx("%next_cutscene_name%", TRUE)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%respawn_heal_cutscene_name%.BCS~ ~.../respawn_heal_cutscene.baf~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_RESURRECTION 
    RET resurrection_spl
BEGIN
    OUTER_SPRINT resurrection_spl ~%RESREF_SPL_RESURRECTION%~

    CREATE ~SPL~ ~%resurrection_spl%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_RAISE_DEAD
            parameter1 = 0
            parameter2 = 0
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CAST_SPELL
            parameter1 = 0 // Casting Level
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~#RDREMOV~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_MAKE_UNSELECTABLE
            END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_HEAL 
    RET heal_spl
BEGIN
    COPY_EXISTING ~SPPR607.SPL~ ~override/%RESREF_SPL_HEAL_HEAL%.SPL~ 
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        BUT_ONLY
    COPY_EXISTING ~SPPR404.SPL~ ~override/%RESREF_SPL_HEAL_CURE%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        BUT_ONLY
    COPY_EXISTING ~SPPR713.SPL~ ~override/%RESREF_SPL_HEAL_RESTORATION%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_FATIGUE_MODIFIER END
        BUT_ONLY
    COPY_EXISTING ~SPPR350.SPL~ ~override/%RESREF_SPL_HEAL_CLARITY%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        BUT_ONLY

    OUTER_SPRINT heal_spl ~%RESREF_SPL_HEAL%~

    CREATE ~SPL~ ~%heal_spl%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_INVISIBILITY
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CURE_PAUSE
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_GRAPHICS_REMOVE_SELECTION_CIRCLE
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_FREEDOM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_FREEDOM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_ALTERNATIVE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_POLYMORPH // Empty resource = Natural form
            parameter1 = 0 // Irrelevant
            parameter2 = 0 // Gain Resistances/statistics
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_ENABLE_BUTTON
            parameter1 = 0 // Irrelevant
            parameter2 = 7 // Button: Talk
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_HEAL%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_CURE%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_RESTORATION%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_HEAL_CLARITY%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPEL_MAGIC
            parameter1 = 0 // Level
            parameter2 = 0 // Always dispel
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = 25820 // Dispel Effects
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SPELL_MAGICAL_REST
            END 
END
