DEFINE_ACTION_FUNCTION APPLY_RESPAWN_PROCESS 
    STR_VAR penalty_cutscene_name = 0
        patrification_state_variable = 0
        instant_death_variable = 0
        kill_player1_variable = 0
    RET_ARRAY respawn_point_names respawn_cutscenes
BEGIN
    LAF APPLY_RESPAWN_RESSURECTION RET resurrection_spl END
    LAF APPLY_RESPAWN_HEAL RET heal_spl END

    COPY - ~%config%/respawn_points.2da~ ~~
        LPF READ_2DA_ROW_NAMES RET_ARRAY respawn_point_names = out END
        PHP_EACH respawn_point_names AS respawn_point_name => _ BEGIN
            LPF READ_2DA_ROW STR_VAR row_name = ~%respawn_point_name%~ 
                RET_ARRAY ~respawn_point_%respawn_point_name%~ = out END
        END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY respawn_cutscenes BEGIN END
    ACTION_PHP_EACH respawn_point_names AS respawn_point_name => respawn_point_index BEGIN
        OUTER_SPRINT respawn_area $~respawn_point_%respawn_point_name%~(AREA)
    
        LAF APPLY_RESPAWN_CUTSCENE INT_VAR respawn_point_index 
            STR_VAR respawn_point_ref = ~respawn_point_%respawn_point_name%~
                resurrection_spl = ~%resurrection_spl%~ 
                heal_spl = ~%heal_spl%~ 
                patrification_state_variable = ~%patrification_state_variable%~
                instant_death_variable = ~%instant_death_variable%~
                kill_player1_variable = ~%kill_player1_variable%~
                next_cutscene_name = ~%penalty_cutscene_name%~
            RET respawn_cutscene_name
            END
        ACTION_DEFINE_ASSOCIATIVE_ARRAY respawn_cutscenes BEGIN ~%respawn_point_name%~ => ~%respawn_cutscene_name%~ END
    END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_RESSURECTION 
    RET resurrection_spl
BEGIN
    OUTER_SPRINT resurrection_spl ~%RESREF_SPL_CR_RESURRECTION%~

    COPY ~%resources%/blank.spl~ ~override/%resurrection_spl%.SPL~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_RAISE_DEAD
            parameter1 = 0
            parameter2 = 0
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_APPLY_EFFECTS_LIST
            parameter1 = 0
            parameter2 = 0
            STR_VAR resource = ~#RDREMOV~
            END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_HEAL 
    RET heal_spl
BEGIN
    OUTER_SPRINT heal_spl ~%RESREF_SPL_CR_HEAL%~

    COPY ~%resources%/blank.spl~ ~override/%heal_spl%.SPL~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CURE_PAUSE
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_INVISIBILITY
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_HP_CURRENT
            parameter1 = 100
            parameter2 = 2 // % mode
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = 25810 // Healed
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_DRUNKENESS
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 5 // Intoxicated (Drunkeness)
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_POISON
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 6 // Poisoned
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_DISEASE
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 7 // Diseased
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_BLINDENESS
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 8 // Blind
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_DEAFNESS
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 112 // Deaf
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_SILENCE
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 34 // Silenced
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_FEEBLEMINDEDNESS
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 48 // Feebleminded
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_LEVEL_DRAIN
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 59 // Drain
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_HORROR
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_STAT_MORALE
            parameter1 = 10 // Default morale
            parameter2 = 1 // Set to value
            special = 1 // BG1 mode
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 36 // Panic
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_CONFUSION
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 2 // Rigid Thinking
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 3 // Confused
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 47 // Chaos
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_SLEEP
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 14 // Sleep
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_BERSERK
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 4 // Berserk
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_STUN
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 55 // Stun
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_CURE_HOLD
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_ICON
            parameter2 = 13 // Held
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPEL_MAGIC
            parameter1 = 0 // Level
            parameter2 = 0 // Always dispel
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = 25820 // Dispel Effects
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SPELL_MAGICAL_REST
            END 
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_CUTSCENE
    INT_VAR respawn_point_index = ~~
    STR_VAR respawn_point_ref = 0
        resurrection_spl = 0
        heal_spl = 0
        patrification_state_variable = 0
        instant_death_variable = 0
        kill_player1_variable = 0
        next_cutscene_name = 0
    RET respawn_cutscene_name
BEGIN
    OUTER_SPRINT respawn_cutscene_name ~%RESREF_BCS_CR_RESPAWN_PROCESS_PREFIX%%respawn_point_index%~
    
    COPY ~%src%/respawn_process.baf~ ~override/%respawn_cutscene_name%.BCS~
        SPRINT area $~%respawn_point_ref%~(AREA)
        SPRINT master_area $~%respawn_point_ref%~(MASTER_AREA)
        SPRINT p1_pos $~%respawn_point_ref%~(PLAYER1_POS)
        SPRINT p2_pos $~%respawn_point_ref%~(PLAYER2_POS)
        SPRINT p3_pos $~%respawn_point_ref%~(PLAYER3_POS)
        SPRINT p4_pos $~%respawn_point_ref%~(PLAYER4_POS)
        SPRINT p5_pos $~%respawn_point_ref%~(PLAYER5_POS)
        SPRINT p6_pos $~%respawn_point_ref%~(PLAYER6_POS)
        SPRINT face $~%respawn_point_ref%~(FACE)
        SPRINT visual_effect ~%VVC_RESREF_RESTORATION%~
        EVALUATE_BUFFER
        COMPILE_BAF_TO_BCS
END
