DEFINE_ACTION_FUNCTION APPLY_RESPAWN_EXECUTION
    STR_VAR respawn_point_names_ref = 0 
        respawn_cutscenes_ref = 0
    RET respawn_execution_cutscene
BEGIN
    LAF APPLY_RESPAWN_PROCESS_EXECUTION STR_VAR respawn_point_names_ref respawn_cutscenes_ref
        RET respawn_point_indicator_variable respawn_process_execution_cutscene
        RET_ARRAY respawn_points_indicators_ref = respawn_points_indicators
        END
    LAF APPLY_RESPAWN_POINT_RESOLVING STR_VAR respawn_point_indicator_variable respawn_points_indicators_ref
        RET respawn_point_resolving_cutscene 
        END

    <<<<<<<< .../start_next_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            StartCutSceneEx("%next_cutscene_name%", TRUE)
    END
    >>>>>>>>

    OUTER_SPRINT respawn_execution_cutscene ~%respawn_point_resolving_cutscene%~

    EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../start_next_cutscene.baf~
        SPRINT next_cutscene_name ~%respawn_process_execution_cutscene%~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_PROCESS_EXECUTION
    STR_VAR respawn_point_names_ref = 0
        respawn_cutscenes_ref = 0
    RET respawn_point_indicator_variable respawn_process_execution_cutscene
    RET_ARRAY respawn_points_indicators
BEGIN
    OUTER_SPRINT respawn_process_execution_cutscene ~%RESREF_BCS_CR_EXECUTION%~

    ACTION_DEFINE_ASSOCIATIVE_ARRAY respawn_points_indicators BEGIN END
    ACTION_PHP_EACH ~%respawn_point_names_ref%~ AS respawn_point_index => respawn_point_name BEGIN
        ACTION_DEFINE_ASSOCIATIVE_ARRAY respawn_points_indicators BEGIN ~%respawn_point_name%~ => ~%respawn_point_index%~ END
    END

    OUTER_SPRINT respawn_point_indicator_variable ~%GLOBAL_CR_RESPAWN_POINT%~

    <<<<<<<< .../execute_respawn_process.baf
    IF
        Global("%respawn_point_indicator_variable%", "GLOBAL", %respawn_point_indicator%)
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            StartCutSceneEx("%respawn_cutscene%", TRUE)
    END
    >>>>>>>>

    ACTION_PHP_EACH respawn_points_indicators AS respawn_point_name => respawn_point_indicator BEGIN
        OUTER_SPRINT respawn_cutscene $~%respawn_cutscenes_ref%~(~%respawn_point_name%~)
        EXTEND_TOP ~%respawn_process_execution_cutscene%.BCS~ ~.../execute_respawn_process.baf~
            EVALUATE_BUFFER
    END
END


DEFINE_ACTION_FUNCTION APPLY_RESPAWN_POINT_RESOLVING
    STR_VAR respawn_point_indicator_variable = 0
        respawn_points_indicators_ref = 0
    RET respawn_point_resolving_cutscene 
BEGIN
    OUTER_SPRINT respawn_point_resolving_cutscene ~%RESREF_BCS_CR_POINT_RESOLVING%~

    <<<<<<<< .../respawn_point_rule.baf
    IF
        %respawn_point_filter%
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SetGlobal("%respawn_point_indicator_variable%", "GLOBAL", %respawn_point_indicator%)
    END
    >>>>>>>>


    /*
        DEFAULT
    */
    EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~True()~
        SPRINT respawn_point_indicator $~%respawn_points_indicators_ref%~(FRIENDLY_ARM_INN)
        EVALUATE_BUFFER


    /*
        NEAREST
    */
    LAF APPLY_NEAREST_RESPAWN_POINT_VARIABLES STR_VAR respawn_points_indicators_ref
        RET nearest_respawn_point_variable 
        END

    ACTION_PHP_EACH ~%respawn_points_indicators_ref%~ AS _ => respawn_point_indicator BEGIN
        EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
            SPRINT respawn_point_filter ~Global("%nearest_respawn_point_variable%", "GLOBAL", %respawn_point_indicator%)~
            SPRINT respawn_point_indicator ~%respawn_point_indicator%~
            EVALUATE_BUFFER
    END


    /*
        PLOT CRITICAL
    */
    LAF APPLY_PLOT_CRITICAL_EXTENSIONS RET candlekeep_enter_variable ice_island_enter_variable END

    EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~Global("Chapter", "GLOBAL", 0)~
        SPRINT respawn_point_indicator $~%respawn_points_indicators_ref%~(CANDLEKEEP_PROLOGUE)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("Chapter", "GLOBAL", 6)
            Global("%candlekeep_enter_variable%", "GLOBAL", 1)
            Global("Criminal", "GLOBAL", 0)
        ~~~~~
        SPRINT respawn_point_indicator $~%respawn_points_indicators_ref%~(CANDLEKEEP)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("Chapter", "GLOBAL", 6)
            Global("Criminal", "GLOBAL", 1)
            Global("Arrested", "GLOBAL", 0)
        ~~~~~
        SPRINT respawn_point_indicator $~%respawn_points_indicators_ref%~(CANDLEKEEP_PRISON)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("Chapter", "GLOBAL", 6)
            Global("Criminal", "GLOBAL", 1)
            Global("Arrested", "GLOBAL", 1)
        ~~~~~
        SPRINT respawn_point_indicator $~%respawn_points_indicators_ref%~(CANDLEKEEP_CATACOMBS)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%respawn_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("%ice_island_enter_variable%", "GLOBAL", 1)
            Global("iceisle", "GLOBAL", 0)
        ~~~~~
        SPRINT respawn_point_indicator $~%respawn_points_indicators_ref%~(ICE_ISLAND)
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_NEAREST_RESPAWN_POINT_VARIABLES
    STR_VAR respawn_points_indicators_ref = 0
    RET nearest_respawn_point_variable
BEGIN
    OUTER_SPRINT nearest_respawn_point_variable ~%GLOBAL_CR_NEAREST_RESPAWN_POINT%~

    COPY - ~%config%/respawn_points_nearest.2da~ ~~
        LPF READ_2DA_COLUMN STR_VAR column_name = ~RESPAWN_POINT~
            RET_ARRAY nearest_respawn_points = out
            END

    <<<<<<<< .../nearest_respawn_point.baf
    IF
        OnCreation()
    THEN
        RESPONSE #100
            SetGlobal("%nearest_respawn_point_variable%", "GLOBAL", %respawn_point_indicator%)
            Continue()
    END
    >>>>>>>>

    ACTION_PHP_EACH nearest_respawn_points AS area_name => respawn_point_name BEGIN
        EXTEND_TOP ~%area_name%.BCS~ ~.../nearest_respawn_point.baf~ 
            SPRINT respawn_point_indicator $~%respawn_points_indicators_ref%~(~%respawn_point_name%~)
            EVALUATE_BUFFER
    END
END


DEFINE_ACTION_FUNCTION APPLY_PLOT_CRITICAL_EXTENSIONS
    RET candlekeep_enter_variable ice_island_enter_variable
BEGIN
    LAF APPLY_CANDLEKEEP_ENTER_EXTENSION RET candlekeep_enter_variable END
    LAF APPLY_CANDLEKEEP_ARRESTED_EXTENSION END
    LAF APPLY_ICE_ISLAND_ENTER_EXTENSION RET ice_island_enter_variable END
END


DEFINE_ACTION_FUNCTION APPLY_CANDLEKEEP_ENTER_EXTENSION
    RET candlekeep_enter_variable
BEGIN
    OUTER_SPRINT candlekeep_enter_variable ~%GLOBAL_CR_CANDLEKEEP_ENTER%~

    <<<<<<<< .../candlekeep_enterence_extension.d
    ADD_TRANS_ACTION KEEPER 
        BEGIN 4 END 
        BEGIN 0 END 
        ~SetGlobal("%candlekeep_enter_variable%", "GLOBAL", 1)~
    >>>>>>>>

    COMPILE ~.../candlekeep_enterence_extension.d~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_CANDLEKEEP_ARRESTED_EXTENSION BEGIN

    <<<<<<<< .../candlekeep_arrested_extension.baf
    IF
        Global("Arrested", "GLOBAL", 0)
    THEN
        RESPONSE #100
            Deactivate("Ulraunt")
            Deactivate("Tethtoril2")
            Wait(5)
            ClearAllActions()
            StartCutSceneMode()
            SetGlobal("Arrested", "GLOBAL", 1)
            StartCutScene("ch6cut02")
    END
    >>>>>>>>

    EXTEND_BOTTOM ~AR2631.BCS~ ~.../candlekeep_arrested_extension.baf~ 
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_ICE_ISLAND_ENTER_EXTENSION
    RET ice_island_enter_variable
BEGIN
    OUTER_SPRINT ice_island_enter_variable ~%GLOBAL_CR_ICE_ISLAND_ENTER%~

    <<<<<<<< .../ice_island_enterence_extension.baf
    IF
        OnCreation()
        Global("%ice_island_enter_variable%", "GLOBAL", 0)
    THEN
        RESPONSE #100
            SetGlobal("%ice_island_enter_variable%", "GLOBAL", 1)
            Continue()
    END
    >>>>>>>>

    EXTEND_TOP ~AR1008.BCS~ ~.../ice_island_enterence_extension.baf~ 
        EVALUATE_BUFFER
END
