COPY ~%resources%/blank.spl~ ~override/%RESREF_SPL_SR_REGENERATION%.SPL~
    LPF ADD_SPL_ABILITY RET ability_index END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_RESTORE_SPELL
        parameter1 = 9 // max level
        parameter2 = 0 // Wizard / Bard / Sorcerer
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_RESTORE_SPELL
        parameter1 = 7 // max level
        parameter2 = 1 // Cleric / Druid / Shaman
        END
    BUT_ONLY


ACTION_CLEAR_ARRAY players
ACTION_DEFINE_ARRAY players BEGIN Player1 Player2 Player3 Player4 Player5 Player6 Familiar END

<<<<<<<< .../spells_regen.baf
IF
    Exists(%player%)
    !TriggerOverride(%player%, ActuallyInCombat())
    !GlobalTimerNotExpired("%timer%","GLOBAL")
THEN
    RESPONSE #100
        ApplySpellRES("%RESREF_SPL_SR_REGENERATION%", %player%)
        SetGlobalTimer("%timer%","GLOBAL","ONE_ROUND")
END
>>>>>>>>

ACTION_PHP_EACH players AS _ => player BEGIN 
    OUTER_SPRINT timer ~%GLOBAL_SR_REGENERATION_TIMER_PREFIX%%player%~
    
    EXTEND_BOTTOM ~BALDUR.BCS~ ~.../spells_regen.baf~ 
        EVALUATE_BUFFER
END
