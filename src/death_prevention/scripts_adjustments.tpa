DEFINE_ACTION_FUNCTION APPLY_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS 
    STR_VAR players_ref = 0 
            game_over_variable = 0
            fake_death_spell = 0
            revive_death_spell = 0
            revive_petrification_spell = 0
            revive_imprisonment_spell = 0
BEGIN
    ACTION_CLEAR_ARRAY patches
    OUTER_SPRINT game_over_action ~~~~~
        SetGlobal("%game_over_variable%", "GLOBAL", 1)
        EndCutSceneMode()
    ~~~~~
    OUTER_SPRINT kill_player1_action ~~~~~
        VerbalConstant(Player1, DYING)
        ReallyForceSpellRES("%fake_death_spell%", Player1)
        %game_over_action%
    ~~~~~
    OUTER_SPRINT revive_action ~~

    ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN
        ~^%s%*HP(Protagonist,1)$~ => ~HP(Protagonist,2)~
        ~^%s%*HPLT(Protagonist,2)$~ => ~HPLT(Protagonist,3)~
        ~^%s%*ChangeStat(Protagonist,CURHITPOINTS,1,SET)$~ => ~ChangeStat(Protagonist,CURHITPOINTS,2,SET)~
        ~^%s%*Kill(Player1)$~ => ~%kill_player1_action%~
        ~^%s%*Kill(Protagonist)$~ => ~%kill_player1_action%~
        ~^%s%*GameOver(.*?)$~ => ~%game_over_action%~
    END

    ACTION_PHP_EACH ~%players_ref%~ AS player_index => player BEGIN 
        ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN 
            ~^%s%*HP(%player%,1)$~ => ~HP(%player%,2)~
            ~^%s%*HPLT(%player%,2)$~ => ~HPLT(%player%,3)~
            ~^%s%*!HPGT(%player%,1)$~ => ~!HPGT(%player%,2)~
            ~^%s%*ChangeStat(%player%,CURHITPOINTS,1,SET)$~ => ~ChangeStat(%player%,CURHITPOINTS,2,SET)~
        END
        ACTION_IF (NOT ~%player%~ STR_EQ ~Player1~) BEGIN 
            ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN 
                ~^%s%*Kill(%player%)$~ => ~~~~~
                    VerbalConstant(%player%, DYING)
                    ReallyForceSpellRES("%fake_death_spell%", %player%)
                ~~~~~
            END
        END
        OUTER_SPRINT revive_action ~~~~~
            %revive_action%
            ApplySpellRES("%revive_death_spell%", %player%)
            ApplySpellRES("%revive_petrification_spell%", %player%)
            ApplySpellRES("%revive_imprisonment_spell%", %player%)
        ~~~~~
    END
    
    ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN
        ~^%s%*StartCutSceneMode()$~ => ~~~~~
            %revive_action%
            StartCutSceneMode()
        ~~~~~
    END

    COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
        LPF PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS STR_VAR patches_ref = patches END
        BUT_ONLY

    COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
        LPF PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS STR_VAR patches_ref = patches END
        BUT_ONLY
END


DEFINE_PATCH_FUNCTION PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS 
    STR_VAR patches_ref = 0
BEGIN 
    DECOMPILE_AND_PATCH BEGIN
        PHP_EACH ~%patches_ref%~ AS pattern => replacement BEGIN 
            REPLACE_EVALUATE CASE_INSENSITIVE ~%pattern%~
                BEGIN 
                    PATCH_LOG ~PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS %SOURCE_EXT% %SOURCE_RES%: Replace %MATCH0%~
                END 
                ~%replacement%~ 
        END
    END
END
