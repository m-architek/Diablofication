DEFINE_ACTION_FUNCTION APPLY_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS 
    STR_VAR players_ref = 0 
            game_over_variable = 0
            kill_player_actions_ref = 0
BEGIN
    ACTION_CLEAR_ARRAY patches
    OUTER_SET patches_not_empty = 0

    OUTER_SPRINT game_over_action ~~~~~
        SetGlobal("%game_over_variable%", "GLOBAL", 1)
        EndCutSceneMode()
    ~~~~~

    ACTION_PHP_EACH ~%players_ref%~ AS player_index => player BEGIN 
        OUTER_SET patches_not_empty = 1
        
        OUTER_SPRINT kill_player_action $~%kill_player_actions_ref%~(~%player_index%~)
        
        ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN 
            ~HP(%player%,1)~ => ~HP(%player%,2)~
            ~HPLT(%player%,2)~ => ~HPLT(%player%,3)~
            ~!HPGT(%player%,1)~ => ~!HPGT(%player%,2)~
            ~ChangeStat(%player%,CURHITPOINTS,1,SET)~ => ~ChangeStat(%player%,CURHITPOINTS,2,SET)~
        END

        ACTION_IF (~%player%~ STR_EQ ~Player1~) BEGIN 
            ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN
                ~HP(Protagonist,1)~ => ~HP(Protagonist,2)~
                ~HPLT(Protagonist,2)~ => ~HPLT(Protagonist,3)~
                ~!HPGT(Protagonist,1)~ => ~!HPGT(Protagonist,2)~
                ~ChangeStat(Protagonist,CURHITPOINTS,1,SET)~ => ~ChangeStat(Protagonist,CURHITPOINTS,2,SET)~
                ~Kill(Player1)~ => ~~~~~
                    %kill_player_action%
                    %game_over_action%
                ~~~~~
                ~Kill(Protagonist)~ => ~~~~~
                    %kill_player_action%
                    %game_over_action%
                ~~~~~
                ~GameOver(.*?)~ => ~%game_over_action%~
            END
        END ELSE BEGIN 
            ACTION_DEFINE_ASSOCIATIVE_ARRAY patches BEGIN 
                ~Kill(%player%)~ => ~%kill_player_action%~
            END
        END
    END

    ACTION_IF (patches_not_empty) BEGIN 

        COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
            LPF PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS STR_VAR patches_ref = patches END
            BUT_ONLY

        COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
            LPF PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS STR_VAR patches_ref = patches END
            BUT_ONLY
    END
END


DEFINE_PATCH_FUNCTION PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS 
    STR_VAR patches_ref = 0
BEGIN 
    DECOMPILE_AND_PATCH BEGIN
        PHP_EACH ~%patches_ref%~ AS pattern => replacement BEGIN 
            REPLACE_EVALUATE CASE_INSENSITIVE ~%pattern%~
                BEGIN 
                    PATCH_LOG ~PATCH_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS %SOURCE_EXT% %SOURCE_RES%: Replace %MATCH0%~
                END 
                ~%replacement%~ 
        END
    END
END
