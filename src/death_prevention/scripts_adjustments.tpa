DEFINE_ACTION_FUNCTION APPLY_DEATH_PREVENTION_SCRIPTS_ADJUSTMENTS 
    STR_VAR players_ref = 0 
            kill_player_variable = 0
BEGIN
    COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_KILL_PLAYER_ACTIONS STR_VAR players_ref kill_player_variable END
            LPF PATCH_1HP_TRIGGERS_AND_ACTIONS STR_VAR players_ref END
        END
        BUT_ONLY

    COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_KILL_PLAYER_ACTIONS STR_VAR players_ref kill_player_variable END
            LPF PATCH_1HP_TRIGGERS_AND_ACTIONS STR_VAR players_ref END
        END
        BUT_ONLY
END

DEFINE_PATCH_FUNCTION PATCH_KILL_PLAYER_ACTIONS 
    STR_VAR players_ref = 0 
            kill_player_variable = 0
BEGIN
    PHP_EACH ~%players_ref%~ AS player_index => player BEGIN 
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Kill(%player%)~ ~~~~~
            ActionOverride(%player%, SetGlobal("%kill_player_variable%", "LOCALS", 1))
        ~~~~~
    END
END

DEFINE_PATCH_FUNCTION PATCH_1HP_TRIGGERS_AND_ACTIONS 
    STR_VAR players_ref = 0 
BEGIN
    PHP_EACH ~%players_ref%~ AS player_index => player BEGIN 
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~HP(%player%,1) ~ ~HP(%player%,2)~
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~HPLT(%player%,2) ~ ~HPLT(%player%,3)~
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ChangeStat(%player%,CURHITPOINTS,1,SET)~ ~ChangeStat(%player%,CURHITPOINTS,2,SET)~
    END
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~HP(Protagonist,1) ~ ~HP(Protagonist,2)~
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~HPLT(Protagonist,2) ~ ~HPLT(Protagonist,3)~
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ChangeStat(Protagonist,CURHITPOINTS,1,SET)~ ~ChangeStat(Protagonist,CURHITPOINTS,2,SET)~
END
