DEFINE_ACTION_FUNCTION APPLY_FAKE_DEATH_SCRIPTS
    STR_VAR players_ref = 0 
            kill_player_variable = 0
            petrify_player_variable = 0
            imprison_player_variable = 0
            death_state_variable = 0
            petrification_state_variable = 0
            imprisonment_state_variable = 0
            kill_player_actions_ref = 0
            petrify_player_actions_ref = 0
            imprison_player_actions_ref = 0
BEGIN
    <<<<<<<< .../apply_fake_death.baf
    IF
        Exists(%player%)
        !StateCheck(%player%, STATE_REALLY_DEAD)
        TriggerOverride(%player%, Global("%death_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%petrification_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%imprisonment_state_variable%", "LOCALS", 0))
        OR(3)
            HP(%player%, 1)
            TriggerOverride(%player%, Global("%kill_player_variable%", "LOCALS", 1))
            TriggerOverride(Player1, False())
        Global("%kill_player_next_tick_variable%", "GLOBAL", 0)
    THEN
        RESPONSE #100
            ClearActions(%player%)
            VerbalConstant(%player%, DYING)
            SetGlobal("%kill_player_next_tick_variable%", "GLOBAL", 1)
    END

    IF
        Exists(%player%)
        Global("%kill_player_next_tick_variable%", "GLOBAL", 1)
    THEN
        RESPONSE #100
            SetGlobal("%kill_player_next_tick_variable%", "GLOBAL", 0)
            %kill_player_action%
    END

    IF
        Exists(%player%)
        !StateCheck(%player%, STATE_REALLY_DEAD)
        TriggerOverride(%player%, Global("%death_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%petrification_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%imprisonment_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%petrify_player_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            %petrify_player_action%
    END

    IF
        Exists(%player%)
        !StateCheck(%player%, STATE_REALLY_DEAD)
        TriggerOverride(%player%, Global("%death_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%petrification_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%imprisonment_state_variable%", "LOCALS", 0))
        TriggerOverride(%player%, Global("%imprison_player_variable%", "LOCALS", 1))
    THEN
        RESPONSE #100
            %imprison_player_action%
    END
    >>>>>>>>
                   
    ACTION_PHP_EACH ~%players_ref%~ AS player_index => player BEGIN 
        OUTER_SPRINT kill_player_action $~%kill_player_actions_ref%~(~%player_index%~)
        OUTER_SPRINT petrify_player_action $~%petrify_player_actions_ref%~(~%player_index%~)
        OUTER_SPRINT imprison_player_action $~%imprison_player_actions_ref%~(~%player_index%~)
        OUTER_SPRINT kill_player_next_tick_variable ~%GLOBAL_KILL_PLAYER_NEXT_TICK_PREFIX%%player_index%~

        LAF EXTEND_GLOBAL_SCRIPT STR_VAR baf = ~.../apply_fake_death.baf~ END
    END
END
