DEFINE_ACTION_FUNCTION APPLY_FAKE_DEATH_RESOURCES
    STR_VAR fake_death_spell = 0 
            fake_petrification_spell = 0 
            fake_imprisonment_spell = 0 
            death_state_variable = 0
            petrification_state_variable = 0
            imprisonment_state_variable = 0
BEGIN
    LAF APPLY_FAKE_DEATH_SPELL STR_VAR fake_death_spell death_state_variable END
    LAF APPLY_FAKE_PETRIFICATION_SPELL STR_VAR fake_petrification_spell petrification_state_variable END
    LAF APPLY_FAKE_IMPRISONMENT_SPELL STR_VAR fake_imprisonment_spell imprisonment_state_variable END
END


DEFINE_ACTION_FUNCTION APPLY_FAKE_DEATH_SPELL 
    STR_VAR fake_death_spell = 0
            death_state_variable = 0
BEGIN 
    COPY_EXISTING - ~efftext.2da~ ~~
        LPF READ_2DA_ROW STR_VAR row_name = ~%EFF_OPCODE_INSTANT_DEATH%~ 
                RET_ARRAY death_efftext = out 
                END
        SET death_strref = $death_efftext(STRREF)
        BUT_ONLY
    
    COPY ~%resources%/death_icon.bam~ ~override/%RESREF_BAM_DEATH_ICON%.BAM~

    LAF APPLY_STATDESC_ENTRY 
        INT_VAR str_ref = death_strref
        STR_VAR icon = ~%RESREF_BAM_DEATH_ICON%~
        RET death_icon = entry
        END

    CREATE ~SPL~ ~%fake_death_spell%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_FAKE_DEATH_COMMON_EFFECTS INT_VAR ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
            parameter1 = 1
            STR_VAR resource = ~%death_state_variable%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_PROTECTION_FROM_OPCODE
            parameter2 = EFF_OPCODE_CURE_SLEEP
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SLEEP
            parameter2 = 1 // Not Wake on Damage
            special = death_icon
            timing = EFF_TIMING_INSTANT_PERMANENT // Allows for removal
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = death_strref
            END
END


DEFINE_ACTION_FUNCTION APPLY_FAKE_PETRIFICATION_SPELL 
    STR_VAR fake_petrification_spell = 0
            petrification_state_variable = 0
BEGIN 
    COPY_EXISTING - ~efftext.2da~ ~~
        LPF READ_2DA_ROW STR_VAR row_name = ~%EFF_OPCODE_STATE_PETRIFICATION%~ 
                RET_ARRAY petrification_efftext = out 
                END
        SET petrification_strref = $petrification_efftext(STRREF)
        BUT_ONLY
    
    COPY ~%resources%/petrification_icon.bam~ ~override/%RESREF_BAM_PETRIFICATION_ICON%.BAM~

    LAF APPLY_STATDESC_ENTRY 
        INT_VAR str_ref = petrification_strref
        STR_VAR icon = ~%RESREF_BAM_PETRIFICATION_ICON%~
        RET petrification_icon = entry
        END

    CREATE ~SPL~ ~%fake_petrification_spell%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_FAKE_DEATH_COMMON_EFFECTS INT_VAR ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
            parameter1 = 1
            STR_VAR resource = ~%petrification_state_variable%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_COLOUR_BY_PALETTE
            parameter1 = 72 // Stone Dead Grey
            parameter2 = 255 // Whole Body
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_PLAY_SOUND
            STR_VAR resource = ~MISC_06B~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_PROTECTION_FROM_OPCODE
            parameter2 = EFF_OPCODE_CURE_SLEEP
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SLEEP
            parameter2 = 1 // Not Wake on Damage
            special = petrification_icon
            timing = EFF_TIMING_INSTANT_PERMANENT // Allows for removal
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = petrification_strref
            END
END


DEFINE_ACTION_FUNCTION APPLY_FAKE_IMPRISONMENT_SPELL 
    STR_VAR fake_imprisonment_spell = 0
            imprisonment_state_variable = 0
BEGIN 
    OUTER_SET imprisonment_strref = "-1"

    ACTION_IF (IDS_OF_SYMBOL (SPELL WIZARD_IMPRISONMENT) != "-1") BEGIN 
        LAF SPELL_IDS_RESOURCE_OF_SYMBOL STR_VAR ids_name = WIZARD_IMPRISONMENT
            RET imprisonment_resource = spell_resource
            END
        COPY_EXISTING - ~%imprisonment_resource%.SPL~ ~~
            READ_LONG NAME1 imprisonment_strref
            BUT_ONLY
    END
    
    COPY ~%resources%/imprisonment_icon.bam~ ~override/%RESREF_BAM_IMPRISONMENT_ICON%.BAM~

    LAF APPLY_STATDESC_ENTRY 
        INT_VAR str_ref = imprisonment_strref
        STR_VAR icon = ~%RESREF_BAM_IMPRISONMENT_ICON%~
        RET imprisonment_icon = entry
        END

    CREATE ~SPL~ ~%fake_imprisonment_spell%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_FAKE_DEATH_COMMON_EFFECTS INT_VAR ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
            parameter1 = 1
            STR_VAR resource = ~%imprisonment_state_variable%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_ICON
            parameter2 = imprisonment_icon
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_DISPLAY_STRING
            parameter1 = imprisonment_strref
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_GRAPHICS_AVATAR_REMOVAL 
            END   
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_GRAPHICS_ANIMATION_REMOVAL 
            parameter2 = 1 // Stat Value
            END   
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_GRAPHICS_REMOVE_SELECTION_CIRCLE
            END
END


DEFINE_PATCH_FUNCTION ADD_FAKE_DEATH_COMMON_EFFECTS 
    INT_VAR ability_index = ~~
BEGIN  
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_STAT_MORALE
        parameter1 = 10
        parameter2 = 1 // Set to value
        special = 1 // Use parameters
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_CURE_INVISIBILITY
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_CURE_PAUSE
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_REMOVE_OPCODE
        parameter1 = "-1"
        parameter2 = EFF_OPCODE_GRAPHICS_REMOVE_SELECTION_CIRCLE
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_REMOVE_OPCODE
        parameter1 = "-1"
        parameter2 = EFF_OPCODE_MAKE_UNSELECTABLE
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_REMOVE_OPCODE
        parameter1 = "-1"
        parameter2 = EFF_OPCODE_FREEDOM
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_FREEDOM
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_POLYMORPH // Empty resource = Natural form
        parameter1 = 0 // Irrelevant
        parameter2 = 0 // Gain Resistances/statistics
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_ENABLE_BUTTON
        parameter1 = 0 // Irrelevant
        parameter2 = 7 // Button: Talk
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_CAST_SPELL
        parameter1 = 0 // Casting Level
        parameter2 = 1 // Cast Instantly at caster level
        STR_VAR resource = ~#RDREMOV~
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_SILENCE
        timing = EFF_TIMING_INSTANT_PERMANENT // Allows for removal
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_STAT_MISCAST_MAGIC
        parameter1 = 100
        parameter2 = 0 // Mage
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_STAT_MISCAST_MAGIC
        parameter1 = 100
        parameter2 = 1 // Cleric
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_STAT_MISCAST_MAGIC
        parameter1 = 100
        parameter2 = 2 // Innate
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_MAKE_UNSELECTABLE
        parameter1 = GAME_IS ~bgee~ // Dialogue disabled (BG:EE: 1, BG2:EE: 0)
        parameter2 = 1 // AI enabled (cannot be disabled in BG:EE)
        END      
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_PROTECTION_FROM_CREATURE_TYPE 
        parameter1 = 255 // ENEMY
        parameter2 = 2 // EA
        END      
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
        opcode = EFF_OPCODE_PROTECTION_FROM_CREATURE_TYPE 
        parameter1 = 128 // NEUTRAL
        parameter2 = 2 // EA
        END
    LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
        opcode = EFF_OPCODE_HP_DAMAGE
        parameter1 = 1
        parameter2 = 0x08000001 // STUNNING Set to Value
        END
END
