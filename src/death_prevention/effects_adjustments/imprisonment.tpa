DEFINE_ACTION_FUNCTION APPLY_IMPRISONMENT_PATCHES
    STR_VAR patches_ref = 0
            revive_imprisonment_spell = 0
    RET_ARRAY ~%patches_ref%~ 
        patch_IMPRISONMENT 
        patch_IMPRISONMENT_IMMUNITY
        patch_IMPRISONMENT_FREEDOM
BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_IMPRISONMENT BEGIN
        patch_type => ~CLONE~
        match_opcode => EFF_OPCODE_IMPRISONMENT
        opcode => EFF_OPCODE_USE_EFF_FILE
        parameter1 => 2 // PC
        parameter2 => 2 // EA
        resource => ~%RESREF_EFF_IMPRISONMENT%~
    END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_IMPRISONMENT_IMMUNITY BEGIN
        patch_type => ~CLONE~
        match_opcode => EFF_OPCODE_PROTECTION_FROM_OPCODE
        match_parameter2 => EFF_OPCODE_IMPRISONMENT
        opcode => EFF_OPCODE_PROTECTION_FROM_SPELL
        parameter1 => 0 // Message
        parameter2 => 0 // Irrelevant
        resource => ~%RESREF_EFF_IMPRISONMENT%~
    END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_IMPRISONMENT_FREEDOM BEGIN
        patch_type => ~CLONE~
        match_opcode => EFF_OPCODE_FREEDOM
        opcode => EFF_OPCODE_APPLY_EFFECTS_LIST
        target => EFF_TARGET_EVERYONE
        parameter1 => 2 // PC
        parameter2 => 102 // EA=n
        resource => ~%revive_imprisonment_spell%~
    END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY ~%patches_ref%~ BEGIN 
        ~%DEATH_PREVENTION_PREFIX%_IMPRISONMENT~ => patch_IMPRISONMENT
        ~%DEATH_PREVENTION_PREFIX%_IMPRISONMENT_IMMUNITY~ => patch_IMPRISONMENT_IMMUNITY
        ~%DEATH_PREVENTION_PREFIX%_IMPRISONMENT_FREEDOM~ => patch_IMPRISONMENT_FREEDOM
    END
END

DEFINE_ACTION_FUNCTION APPLY_IMPRISONMENT_RESOURCES 
    STR_VAR imprison_player_variable = 0
BEGIN
    CREATE ~EFF~ ~%RESREF_EFF_IMPRISONMENT%~
        LPF ALTER_EFF_V2_EFFECT
            INT_VAR opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
                parameter1 = 1
                parent_resource_type = EFF_V2_PARENT_RESOURCE_TYPE_SPELL
            STR_VAR resource = ~%imprison_player_variable%~
                parent_resource = ~%RESREF_EFF_IMPRISONMENT%~
            END
END