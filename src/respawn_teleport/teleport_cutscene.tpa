DEFINE_ACTION_FUNCTION APPLY_RESPAWN_TELEPORT_CUTSCENE 
    INT_VAR teleport_next_cutscene_variable_value = ~~
    STR_VAR teleport_next_cutscene_name = 0
    RET teleport_cutscene_name 
        teleport_next_cutscene_variable_name
BEGIN 
    OUTER_SPRINT teleport_cutscene_name ~%RESREF_BCS_TELEPORT_POINT_RESOLVING%~
    OUTER_SPRINT teleport_next_cutscene_resolving_cutscene_name ~%RESREF_BCS_TELEPORT_NEXT_CUTSCENE_RESOLVING%~
    OUTER_SPRINT teleport_next_cutscene_variable_name ~%GLOBAL_TELEPORT_NEXT_CUTSCENE%~
    
    ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%teleport_cutscene_name%.BCS~) BEGIN 
        LAF APPLY_TELEPORT_CUTSCENES STR_VAR next_cutscene_name = ~%teleport_next_cutscene_resolving_cutscene_name%~
            RET_ARRAY respawn_point_names teleport_cutscenes
            END

        LAF APPLY_TELEPORT_EXECUTION STR_VAR respawn_point_names_ref = respawn_point_names 
                teleport_cutscenes_ref = teleport_cutscenes
            RET respawn_point_variable 
                teleport_execution_cutscene_name
            END
        
        LAF APPLY_TELEPORT_POINT_RESOLVING STR_VAR respawn_point_variable 
                respawn_point_names_ref = respawn_point_names 
                this_cutscene_name = ~%teleport_cutscene_name%~
                next_cutscene_name = ~%teleport_execution_cutscene_name%~
            END
    END

    <<<<<<<< .../teleport_next_cutscene_rule.baf
    IF
        Global("%teleport_next_cutscene_variable_name%", "GLOBAL", %teleport_next_cutscene_variable_value%)
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            StartCutSceneEx("%teleport_next_cutscene_name%", TRUE)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%teleport_next_cutscene_resolving_cutscene_name%.BCS~ ~.../teleport_next_cutscene_rule.baf~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_TELEPORT_CUTSCENES
    STR_VAR next_cutscene_name = 0
    RET_ARRAY respawn_point_names teleport_cutscenes
BEGIN 
    <<<<<<<< .../teleport_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)

            MakeUnselectable(0)
            ActionOverride(Player2, MakeUnselectable(0))
            ActionOverride(Player3, MakeUnselectable(0))
            ActionOverride(Player4, MakeUnselectable(0))
            ActionOverride(Player5, MakeUnselectable(0))
            ActionOverride(Player6, MakeUnselectable(0))

            LeaveAreaLUAPanic("%area%", "", %p1_pos%, %face%)
            LeaveAreaLUA("%area%", "", %p1_pos%, %face%)
            ActionOverride(Player2, LeaveAreaLUA("%area%", "", %p2_pos%, %face%))
            ActionOverride(Player3, LeaveAreaLUA("%area%", "", %p3_pos%, %face%))
            ActionOverride(Player4, LeaveAreaLUA("%area%", "", %p4_pos%, %face%))
            ActionOverride(Player5, LeaveAreaLUA("%area%", "", %p5_pos%, %face%))
            ActionOverride(Player6, LeaveAreaLUA("%area%", "", %p6_pos%, %face%))
            SetMasterArea("%master_area%")
            MultiPlayerSync()
            
            StartCutSceneEx("%next_cutscene_name%", TRUE)
    END
    >>>>>>>>

    ACTION_CLEAR_ARRAY respawn_point_names
    ACTION_CLEAR_ARRAY teleport_cutscenes

    COPY - ~%config%/respawn_points.2da~ ~~
        LPF READ_2DA_ROW_NAMES RET_ARRAY respawn_point_names = out END
        
        PHP_EACH respawn_point_names AS respawn_point_name => respawn_point_index BEGIN
            CLEAR_ARRAY respawn_point
            LPF READ_2DA_ROW STR_VAR row_name = ~%respawn_point_name%~ 
                RET_ARRAY respawn_point = out END

            SPRINT teleport_cutscene_name ~%RESREF_BCS_TELEPORT_PREFIX%%respawn_point_index%~
            
            INNER_ACTION BEGIN 
                EXTEND_BOTTOM ~%teleport_cutscene_name%.BCS~ ~.../teleport_cutscene.baf~
                    SPRINT area $respawn_point(AREA)
                    SPRINT master_area $respawn_point(MASTER_AREA)
                    SPRINT p1_pos $respawn_point(PLAYER1_POS)
                    SPRINT p2_pos $respawn_point(PLAYER2_POS)
                    SPRINT p3_pos $respawn_point(PLAYER3_POS)
                    SPRINT p4_pos $respawn_point(PLAYER4_POS)
                    SPRINT p5_pos $respawn_point(PLAYER5_POS)
                    SPRINT p6_pos $respawn_point(PLAYER6_POS)
                    SPRINT face $respawn_point(FACE)
                    EVALUATE_BUFFER
            END
        
            DEFINE_ASSOCIATIVE_ARRAY teleport_cutscenes BEGIN ~%respawn_point_name%~ => ~%teleport_cutscene_name%~ END
        END
END


DEFINE_ACTION_FUNCTION APPLY_TELEPORT_EXECUTION
    STR_VAR respawn_point_names_ref = 0
        teleport_cutscenes_ref = 0
        next_cutscene_name = 0
    RET respawn_point_variable 
        teleport_execution_cutscene_name
BEGIN
    OUTER_SPRINT teleport_execution_cutscene_name ~%RESREF_BCS_TELEPORT_EXECUTION%~
    OUTER_SPRINT respawn_point_variable ~%GLOBAL_RESPAWN_POINT%~

    <<<<<<<< .../teleport_execution.baf
    IF
        Global("%respawn_point_variable%", "GLOBAL", %respawn_point_index%)
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            StartCutSceneEx("%teleport_cutscene%", TRUE)
    END
    >>>>>>>>

    ACTION_PHP_EACH ~%respawn_point_names_ref%~ AS respawn_point_name => respawn_point_index BEGIN
        OUTER_SPRINT teleport_cutscene $~%teleport_cutscenes_ref%~(~%respawn_point_name%~)
        EXTEND_TOP ~%teleport_execution_cutscene_name%.BCS~ ~.../teleport_execution.baf~
            EVALUATE_BUFFER
    END
END


DEFINE_ACTION_FUNCTION APPLY_TELEPORT_POINT_RESOLVING
    STR_VAR respawn_point_variable = 0
        respawn_point_names_ref = 0
        this_cutscene_name = 0
        next_cutscene_name = 0
BEGIN
    OUTER_SPRINT teleport_point_resolving_cutscene ~%this_cutscene_name%~

    <<<<<<<< .../respawn_point_rule.baf
    IF
        %respawn_point_filter%
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SetGlobal("%respawn_point_variable%", "GLOBAL", %respawn_point_index%)
    END
    >>>>>>>>


    // Apply nearest point rules

    LAF APPLY_NEAREST_RESPAWN_POINT_VARIABLES STR_VAR respawn_point_names_ref
        RET nearest_respawn_point_variable 
        END

    ACTION_PHP_EACH ~%respawn_point_names_ref%~ AS _ => respawn_point_index BEGIN
        EXTEND_BOTTOM ~%teleport_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
            SPRINT respawn_point_filter ~Global("%nearest_respawn_point_variable%", "GLOBAL", %respawn_point_index%)~
            EVALUATE_BUFFER
    END


    // Apply plot critical rules

    LAF APPLY_PLOT_CRITICAL_EXTENSIONS RET candlekeep_enter_variable ice_island_enter_variable END

    EXTEND_BOTTOM ~%teleport_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~Global("Chapter", "GLOBAL", 0)~
        SPRINT respawn_point_index $~%respawn_point_names_ref%~(CANDLEKEEP_PROLOGUE)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%teleport_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("Chapter", "GLOBAL", 6)
            Global("%candlekeep_enter_variable%", "GLOBAL", 1)
            Global("Criminal", "GLOBAL", 0)
        ~~~~~
        SPRINT respawn_point_index $~%respawn_point_names_ref%~(CANDLEKEEP)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%teleport_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("Chapter", "GLOBAL", 6)
            Global("Criminal", "GLOBAL", 1)
            Global("Arrested", "GLOBAL", 0)
        ~~~~~
        SPRINT respawn_point_index $~%respawn_point_names_ref%~(CANDLEKEEP_PRISON)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%teleport_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("Chapter", "GLOBAL", 6)
            Global("Criminal", "GLOBAL", 1)
            Global("Arrested", "GLOBAL", 1)
        ~~~~~
        SPRINT respawn_point_index $~%respawn_point_names_ref%~(CANDLEKEEP_CATACOMBS)
        EVALUATE_BUFFER

    EXTEND_BOTTOM ~%teleport_point_resolving_cutscene%.BCS~ ~.../respawn_point_rule.baf~ 
        SPRINT respawn_point_filter ~~~~~
            Global("%ice_island_enter_variable%", "GLOBAL", 1)
            Global("iceisle", "GLOBAL", 0)
        ~~~~~
        SPRINT respawn_point_index $~%respawn_point_names_ref%~(ICE_ISLAND)
        EVALUATE_BUFFER


    // Execute next cutscene

    <<<<<<<< .../start_next_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            StartCutSceneEx("%next_cutscene_name%", TRUE)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%teleport_point_resolving_cutscene%.BCS~ ~.../start_next_cutscene.baf~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_NEAREST_RESPAWN_POINT_VARIABLES
    STR_VAR respawn_point_names_ref = 0
    RET nearest_respawn_point_variable
BEGIN
    OUTER_SPRINT nearest_respawn_point_variable ~%GLOBAL_NEAREST_RESPAWN_POINT%~

    COPY - ~%config%/respawn_points_nearest.2da~ ~~
        LPF READ_2DA_COLUMN STR_VAR column_name = ~RESPAWN_POINT~
            RET_ARRAY nearest_respawn_points = out
            END

    <<<<<<<< .../nearest_respawn_point.baf
    IF
        OnCreation()
    THEN
        RESPONSE #100
            SetGlobal("%nearest_respawn_point_variable%", "GLOBAL", %respawn_point_index%)
            Continue()
    END
    >>>>>>>>

    ACTION_PHP_EACH nearest_respawn_points AS area_name => respawn_point_name BEGIN
        EXTEND_TOP ~%area_name%.BCS~ ~.../nearest_respawn_point.baf~ 
            SPRINT respawn_point_index $~%respawn_point_names_ref%~(~%respawn_point_name%~)
            EVALUATE_BUFFER
    END
END


DEFINE_ACTION_FUNCTION APPLY_PLOT_CRITICAL_EXTENSIONS
    RET candlekeep_enter_variable ice_island_enter_variable
BEGIN
    LAF APPLY_CANDLEKEEP_ENTER_EXTENSION RET candlekeep_enter_variable END
    LAF APPLY_CANDLEKEEP_ARRESTED_EXTENSION END
    LAF APPLY_ICE_ISLAND_ENTER_EXTENSION RET ice_island_enter_variable END
END


DEFINE_ACTION_FUNCTION APPLY_CANDLEKEEP_ENTER_EXTENSION
    RET candlekeep_enter_variable
BEGIN
    OUTER_SPRINT candlekeep_enter_variable ~%GLOBAL_CANDLEKEEP_ENTER%~

    COPY_EXISTING ~CH6CUT01.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~CutSceneId(Player1)~ ~~~~~
                CutSceneId(Player1)
                SetGlobal("%candlekeep_enter_variable%", "GLOBAL", 1)
            ~~~~~
        END
        BUT_ONLY
END


DEFINE_ACTION_FUNCTION APPLY_CANDLEKEEP_ARRESTED_EXTENSION BEGIN

    <<<<<<<< .../candlekeep_arrested_extension.baf
    IF
        Global("Arrested", "GLOBAL", 0)
    THEN
        RESPONSE #100
            ClearAllActions()
            StartCutSceneMode()
            SetGlobal("Arrested", "GLOBAL", 1)
            StartCutScene("ch6cut02")
    END
    >>>>>>>>

    EXTEND_BOTTOM ~AR2631.BCS~ ~.../candlekeep_arrested_extension.baf~ 
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_ICE_ISLAND_ENTER_EXTENSION
    RET ice_island_enter_variable
BEGIN
    OUTER_SPRINT ice_island_enter_variable ~%GLOBAL_ICE_ISLAND_ENTER%~

    <<<<<<<< .../ice_island_enterence_extension.baf
    IF
        OnCreation()
        Global("%ice_island_enter_variable%", "GLOBAL", 0)
    THEN
        RESPONSE #100
            SetGlobal("%ice_island_enter_variable%", "GLOBAL", 1)
            Continue()
    END
    >>>>>>>>

    EXTEND_TOP ~AR1008.BCS~ ~.../ice_island_enterence_extension.baf~ 
        EVALUATE_BUFFER
END
