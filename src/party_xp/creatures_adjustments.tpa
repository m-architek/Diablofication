DEFINE_ACTION_FUNCTION APPLY_CREATURES_ADJUSTMENTS BEGIN
    PRINT ~~
    PRINT ~Patching existing creatures. This can take a while...~
    PRINT ~~

    LAF JOINABLE_NPC_ARRAY RET_ARRAY JOINABLE_NPC_ARRAY END

    ACTION_CLEAR_ARRAY party_xp_scripts
    ACTION_CLEAR_ARRAY party_xp_parameters

    LAF COUNT_EXISTING STR_VAR glob = ~^.+\.cre$~ RET count END
    OUTER_SET counter = 0

    COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~

        PATCH_IF (NOT VARIABLE_IS_SET $JOINABLE_NPC_ARRAY(~%SOURCE_FILE%~)) BEGIN 
            LPF READ_CRE_XP_VALUE RET xp_value END

            PATCH_IF (xp_value > 0) BEGIN 
                
                LPF RESOLVE_CRE_OVERRIDE_SCRIPT
                    STR_VAR prefix = ~%RESREF_BCS_PREFIX%~
                    RET script_resref 
                    END

                SPRINT $party_xp_parameters(~%script_resref%~) ~%xp_value%~
                SPRINT $party_xp_scripts(~%script_resref%~) ~~

                LPF ADD_CRE_EFFECT INT_VAR opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR 
                    parameter1 = xp_value
                    parameter2 = 0 // Set
                    timing = EFF_TIMING_INSTANT_PERMANENT
                    STR_VAR resource = ~%LOCALS_PARTY_XP_VALUE%~
                    END  

                LPF PATCH_CRE_XP_VALUE INT_VAR xp_value = 0 END
            END
        END
        SET counter = counter + 1
        LPF PRINT_PROGRESS INT_VAR count counter END
        BUT_ONLY


    ACTION_PHP_EACH party_xp_parameters AS script_resref => xp_value BEGIN 

        LAF MATH_ROUND_DOWN_TO_SIGFIGS 
            INT_VAR value = (xp_value / MAX_IN_PARTY) 
                    sigfigs = 2
            RET calculated_xp = out
            END
        ACTION_IF (calculated_xp = 0) BEGIN 
            OUTER_SET calculated_xp = 1
        END

        OUTER_SPRINT party_xp_script $party_xp_scripts(~%script_resref%~)

        OUTER_SPRINT party_xp_script ~%party_xp_script%~ ^ ~~~~~
            IF
                Die()
                Allegiance(Myself, ENEMY)
                CheckStat(Myself, 0, XPVALUE)
                Global("%LOCALS_PARTY_XP_VALUE%", "LOCALS", %xp_value%)
            THEN
                RESPONSE #100
                    AddXPObject(Player1,%calculated_xp%)
                    AddXPObject(Player2,%calculated_xp%)
                    AddXPObject(Player3,%calculated_xp%)
                    AddXPObject(Player4,%calculated_xp%)
                    AddXPObject(Player5,%calculated_xp%)
                    AddXPObject(Player6,%calculated_xp%)
                    Continue() 
            END
        ~~~~~
        
        LOG ~Extending script %script_resref% for XP value %xp_value%. Calculated XP: %calculated_xp%~

        OUTER_SPRINT $party_xp_scripts(~%script_resref%~) ~%party_xp_script%~
    END

    ACTION_PHP_EACH party_xp_scripts AS script_resref => party_xp_script BEGIN 
        LAF APPEND_BCS_TOP STR_VAR resource = ~%script_resref%~ script = ~%party_xp_script%~ END
    END
END
