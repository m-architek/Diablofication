DEFINE_ACTION_FUNCTION APPLY_PORTAL_SPELL
    STR_VAR portal_cutscene = 0
    RET portal_spell portal_owner_local_variable
BEGIN     
    OUTER_SPRINT portal_owner_local_variable ~%LOCALS_PORTAL_OWNER%~

    LAF EXTEND_CUT250_WITH_CUTSCENE STR_VAR cutscene = ~%portal_cutscene%~
        RET cut250_index
        END

    LAF SPELL_IDS_ADD_SPELL INT_VAR spell_type = IDS_SPELL_TYPE_WIZARD spell_level = 1
        STR_VAR ids_name = ~%SPELL_IDS_TOWN_PORTAL%~
        RET portal_spell = spell_resource
        END

    CREATE ~SPL~ ~%portal_spell%~
        SAY NAME1 @1
        SAY UNIDENTIFIED_DESC @2
        LPF ADD_SPL_HEADER INT_VAR spell_type = SPL_SPELL_TYPE_WIZARD
                exclusion = SPL_EXCLUSION_ABJURERS
                animation = SPL_ANIMATION_ALTERATION
                school = SPL_SCHOOL_TRANSMUTER
                secondary_type = SPL_SECONDARY_TYPE_NON_COMBAT
                level = 1
            STR_VAR sound = ~CAS_M08~
                icon = ~SPWI402C~
            END
        LPF ADD_SPL_ABILITY INT_VAR location = ITM_SPL_ABILITY_LOCATION_SPELL
                target = ITM_SPL_ABILITY_TARGET_CASTER
                range = 0
                level_required = 1
                casting_time = 9
            STR_VAR icon = ~SPWI402C~
            RET ability_index 
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
                opcode = EFF_OPCODE_SCRIPT_MODIFY_LOCAL_VAR
                target = EFF_TARGET_SELF
                parameter1 = 1
            STR_VAR resource = ~%portal_owner_local_variable%~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
                opcode = EFF_OPCODE_SCRIPT_MODIFY_GLOBAL_VAR
                target = EFF_TARGET_SELF
                parameter1 = cut250_index
            STR_VAR resource = ~CUT250~
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
                opcode = EFF_OPCODE_EXECUTE_CUT250
                target = EFF_TARGET_SELF
            END
END
