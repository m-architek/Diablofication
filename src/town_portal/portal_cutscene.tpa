DEFINE_ACTION_FUNCTION APPLY_PORTAL_CUTSCENE 
    RET cut250_index 
BEGIN 
    OUTER_SET teleport_next_cutscene_variable_value = 1

    LAF APPLY_PORTAL_LEAVE_CUTSCENE RET portal_leave_cutscene_name END

    LAF APPLY_RESPAWN_TELEPORT INT_VAR teleport_next_cutscene_variable_value
        STR_VAR teleport_next_cutscene_name = ~%portal_leave_cutscene_name%~
        RET teleport_cutscene_name 
            teleport_next_cutscene_variable_name
        END

    LAF APPLY_PORTAL_ENTER_CUTSCENE INT_VAR teleport_next_cutscene_variable_value
        STR_VAR teleport_cutscene_name 
            teleport_next_cutscene_variable_name
        RET portal_enter_cutscene_name
        END

    LAF APPLY_CUT250_PORTAL_EXTENSION STR_VAR portal_enter_cutscene_name
        RET cut250_index
        END
END


DEFINE_ACTION_FUNCTION APPLY_CUT250_PORTAL_EXTENSION 
    STR_VAR portal_enter_cutscene_name = 0
    RET cut250_index 
BEGIN 
    <<<<<<<< .../cut250_portal_custscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            StartCutSceneEx("%portal_enter_cutscene_name%",TRUE)
    END
    >>>>>>>>

    LAF EXTEND_CUT250 STR_VAR baf_file = ~.../cut250_portal_custscene.baf~
        RET cut250_index = value
        END

    ACTION_IF (NOT cut250_index) BEGIN 
        FAIL ~Cannot extend CUT250 with portal cutscene.~
    END
END


DEFINE_ACTION_FUNCTION APPLY_PORTAL_ENTER_CUTSCENE
    INT_VAR teleport_next_cutscene_variable_value = ~~
    STR_VAR teleport_cutscene_name = 0
        teleport_next_cutscene_variable_name = 0
    RET portal_enter_cutscene_name
BEGIN 
    OUTER_SPRINT portal_enter_cutscene_name ~%RESREF_BCS_PORTAL_ENTER_CUTSCENE%~

    LAF APPLY_PORTAL_CLARITY_SPELL RET clarity_spl END

    <<<<<<<< .../portal_enter_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            ClearAllActions()

            ApplySpell(Player1, DRYAD_TELEPORT)
            ApplySpell(Player2, DRYAD_TELEPORT)
            ApplySpell(Player3, DRYAD_TELEPORT)
            ApplySpell(Player4, DRYAD_TELEPORT)
            ApplySpell(Player5, DRYAD_TELEPORT)
            ApplySpell(Player6, DRYAD_TELEPORT)
  
            SmallWait(45)
            FadeToColor([30.0],0)
            SmallWait(30)

            ApplySpellRES("%clarity_spl%", Player1)
            ApplySpellRES("%clarity_spl%", Player2)
            ApplySpellRES("%clarity_spl%", Player3)
            ApplySpellRES("%clarity_spl%", Player4)
            ApplySpellRES("%clarity_spl%", Player5)
            ApplySpellRES("%clarity_spl%", Player6)

            SetGlobal("%teleport_next_cutscene_variable_name%", "GLOBAL", %teleport_next_cutscene_variable_value%)
            StartCutSceneEx("%teleport_cutscene_name%",TRUE)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%portal_enter_cutscene_name%.BCS~ ~.../portal_enter_cutscene.baf~
        EVALUATE_BUFFER
END 


DEFINE_ACTION_FUNCTION APPLY_PORTAL_LEAVE_CUTSCENE 
    RET portal_leave_cutscene_name
BEGIN 
    OUTER_SPRINT portal_leave_cutscene_name ~%RESREF_BCS_PORTAL_LEAVE_CUTSCENE%~

    <<<<<<<< .../portal_leave_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SmallWait(15)
            CreateVisualEffectObject("spdimndr", Player1)
            CreateVisualEffectObject("spdimndr", Player2)
            CreateVisualEffectObject("spdimndr", Player3)
            CreateVisualEffectObject("spdimndr", Player4)
            CreateVisualEffectObject("spdimndr", Player5)
            CreateVisualEffectObject("spdimndr", Player6)
            SmallWait(15)
            FadeFromColor([30.0],0)
            SmallWait(30)
            EndCutSceneMode()
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%portal_leave_cutscene_name%.BCS~ ~.../portal_leave_cutscene.baf~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_PORTAL_CLARITY_SPELL 
    RET clarity_spl
BEGIN
    COPY_EXISTING ~SPPR350.SPL~ ~override/%RESREF_SPL_PORTAL_CLARITY_SUBSPELL%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_COLOR_PULSE END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_PLAY_SOUND END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_GRPAHICS_PLAY_EFFECT END
        BUT_ONLY

    OUTER_SPRINT clarity_spl ~%RESREF_SPL_PORTAL_CLARITY%~

    CREATE ~SPL~ ~%clarity_spl%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_ALTERNATIVE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_FREEDOM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_FREEDOM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_MAKE_UNSELECTABLE
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_PORTAL_CLARITY_SUBSPELL%~
            END
END
