DEFINE_ACTION_FUNCTION APPLY_PORTAL_CUTSCENE 
    STR_VAR portal_begin_cutscene = 0 
        portal_owner_local_variable = 0
BEGIN 
    OUTER_SPRINT portal_end_cutscene ~%RESREF_BCS_PORTAL_END%~

    OUTER_SET portal_next_cutscene_index = 1

    LAF APPLY_RESPAWN_PORTAL INT_VAR next_cutscene_index = ~%portal_next_cutscene_index%~
        STR_VAR next_cutscene = ~%portal_end_cutscene%~
        RET portal_cutscene
            portal_owner_variable
            portal_next_cutscene_variable = next_cutscene_variable
        END

    LAF APPLY_PORTAL_BEGIN INT_VAR portal_next_cutscene_index
        STR_VAR portal_owner_variable
            portal_owner_local_variable
            portal_next_cutscene_variable
            this_cutscene = ~%portal_begin_cutscene%~
            next_cutscene = ~%portal_cutscene%~
        END

    LAF APPLY_PORTAL_END
        STR_VAR portal_owner_local_variable
            this_cutscene = ~%portal_end_cutscene%~
        END
END


DEFINE_ACTION_FUNCTION APPLY_PORTAL_BEGIN
    INT_VAR portal_next_cutscene_index = ~~
    STR_VAR portal_owner_variable = 0
        portal_owner_local_variable = 0
        portal_next_cutscene_variable = 0
        this_cutscene = 0
        next_cutscene = 0
BEGIN 

    <<<<<<<< .../portal_begin.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            ClearAllActions()

            ApplySpell(Player1, DRYAD_TELEPORT)
            ApplySpell(Player2, DRYAD_TELEPORT)
            ApplySpell(Player3, DRYAD_TELEPORT)
            ApplySpell(Player4, DRYAD_TELEPORT)
            ApplySpell(Player5, DRYAD_TELEPORT)
            ApplySpell(Player6, DRYAD_TELEPORT)
  
            SmallWait(45)
            FadeToColor([30.0],0)
            SmallWait(30)
            SetGlobal("%portal_next_cutscene_variable%", "GLOBAL", %portal_next_cutscene_index%)
            SetGlobal("%portal_owner_variable%", "GLOBAL", 0)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../portal_begin.baf~
        EVALUATE_BUFFER

    <<<<<<<< .../portal_owner_detection.baf
    IF
        Exists(%player%)
        !StateCheck(%player%, STATE_REALLY_DEAD)
        TriggerOverride(%player%, Global("%portal_owner_local_variable%", "LOCALS", %player_index%))
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SetGlobal("%portal_owner_variable%", "GLOBAL", %player_index%)
    END
    >>>>>>>>

    ACTION_PHP_EACH PLAYERS AS player_index => player BEGIN 
        EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../portal_owner_detection.baf~
                EVALUATE_BUFFER
    END

    LAF START_NEXT_CUTSCENE STR_VAR this_cutscene next_cutscene END
END 


DEFINE_ACTION_FUNCTION APPLY_PORTAL_END
    STR_VAR portal_owner_local_variable = 0
        this_cutscene = 0
BEGIN 
    <<<<<<<< .../portal_end.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SetSequence(SEQ_READY)
            ActionOverride(Player2, SetSequence(SEQ_READY))
            ActionOverride(Player3, SetSequence(SEQ_READY))
            ActionOverride(Player4, SetSequence(SEQ_READY))
            ActionOverride(Player5, SetSequence(SEQ_READY))
            ActionOverride(Player6, SetSequence(SEQ_READY))
            
            SetGlobal("%portal_owner_local_variable%", "LOCALS", 0)
            ActionOverride(Player2, SetGlobal("%portal_owner_local_variable%", "LOCALS", 0))
            ActionOverride(Player3, SetGlobal("%portal_owner_local_variable%", "LOCALS", 0))
            ActionOverride(Player4, SetGlobal("%portal_owner_local_variable%", "LOCALS", 0))
            ActionOverride(Player5, SetGlobal("%portal_owner_local_variable%", "LOCALS", 0))
            ActionOverride(Player6, SetGlobal("%portal_owner_local_variable%", "LOCALS", 0))

            ApplySpell(Player1, DRYAD_TELEPORT)
            ApplySpell(Player2, DRYAD_TELEPORT)
            ApplySpell(Player3, DRYAD_TELEPORT)
            ApplySpell(Player4, DRYAD_TELEPORT)
            ApplySpell(Player5, DRYAD_TELEPORT)
            ApplySpell(Player6, DRYAD_TELEPORT)

            SmallWait(15)
            FadeFromColor([30.0],0)
            SmallWait(30)
            EndCutSceneMode()
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../portal_end.baf~
        EVALUATE_BUFFER
END
