DEFINE_ACTION_FUNCTION APPLY_PORTAL_CUTSCENE 
    STR_VAR portal_begin_cutscene = 0 
        portal_onwer_local_varable = 0
BEGIN 
    OUTER_SPRINT portal_end_cutscene ~%RESREF_BCS_PORTAL_END%~

    OUTER_SET portal_next_cutscene_index = 1

    LAF APPLY_RESPAWN_PORTAL INT_VAR next_cutscene_index = ~%portal_next_cutscene_index%~
        STR_VAR next_cutscene = ~%portal_end_cutscene%~
        RET portal_cutscene
            portal_owner_variable
            portal_next_cutscene_variable = next_cutscene_variable
        END

    LAF APPLY_PORTAL_BEGIN INT_VAR portal_next_cutscene_index
        STR_VAR portal_owner_variable
            portal_onwer_local_varable
            portal_next_cutscene_variable
            this_cutscene = ~%portal_begin_cutscene%~
            next_cutscene = ~%portal_cutscene%~
        END

    LAF APPLY_PORTAL_END
        STR_VAR portal_onwer_local_varable
            this_cutscene = ~%portal_end_cutscene%~
        END
END


DEFINE_ACTION_FUNCTION APPLY_PORTAL_BEGIN
    INT_VAR portal_next_cutscene_index = ~~
    STR_VAR portal_owner_variable = 0
        portal_onwer_local_varable = 0
        portal_next_cutscene_variable = 0
        this_cutscene = 0
        next_cutscene = 0
BEGIN 
    LAF APPLY_PORTAL_CLARITY_SPELL RET clarity_spl END

    <<<<<<<< .../portal_begin.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            ClearAllActions()

            ApplySpell(Player1, DRYAD_TELEPORT)
            ApplySpell(Player2, DRYAD_TELEPORT)
            ApplySpell(Player3, DRYAD_TELEPORT)
            ApplySpell(Player4, DRYAD_TELEPORT)
            ApplySpell(Player5, DRYAD_TELEPORT)
            ApplySpell(Player6, DRYAD_TELEPORT)
  
            SmallWait(45)
            FadeToColor([30.0],0)
            SmallWait(30)

            ApplySpellRES("%clarity_spl%", Player1)
            ApplySpellRES("%clarity_spl%", Player2)
            ApplySpellRES("%clarity_spl%", Player3)
            ApplySpellRES("%clarity_spl%", Player4)
            ApplySpellRES("%clarity_spl%", Player5)
            ApplySpellRES("%clarity_spl%", Player6)

            SetGlobal("%portal_next_cutscene_variable%", "GLOBAL", %portal_next_cutscene_index%)
            SetGlobal("%portal_owner_variable%", "GLOBAL", 0)
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../portal_begin.baf~
        EVALUATE_BUFFER

    <<<<<<<< .../portal_owner_detection.baf
    IF
        Exists(%player%)
        !StateCheck(%player%, STATE_REALLY_DEAD)
        TriggerOverride(%player%, Global("%portal_onwer_local_varable%", "LOCALS", %player_index%))
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            SetGlobal("%portal_owner_variable%", "GLOBAL", %player_index%)
    END
    >>>>>>>>

    ACTION_PHP_EACH PLAYERS AS player_index => player BEGIN 
        EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../portal_owner_detection.baf~
                EVALUATE_BUFFER
    END

    LAF START_NEXT_CUTSCENE STR_VAR this_cutscene next_cutscene END
END 


DEFINE_ACTION_FUNCTION APPLY_PORTAL_END
    STR_VAR portal_onwer_local_varable = 0
        this_cutscene = 0
BEGIN 
    <<<<<<<< .../portal_end.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            
            SetGlobal("%portal_onwer_local_varable%", "LOCALS", 0)
            ActionOverride(Player2, SetGlobal("%portal_onwer_local_varable%", "LOCALS", 0))
            ActionOverride(Player3, SetGlobal("%portal_onwer_local_varable%", "LOCALS", 0))
            ActionOverride(Player4, SetGlobal("%portal_onwer_local_varable%", "LOCALS", 0))
            ActionOverride(Player5, SetGlobal("%portal_onwer_local_varable%", "LOCALS", 0))
            ActionOverride(Player6, SetGlobal("%portal_onwer_local_varable%", "LOCALS", 0))
            
            CreateVisualEffectObject("spdimndr", Player1)
            CreateVisualEffectObject("spdimndr", Player2)
            CreateVisualEffectObject("spdimndr", Player3)
            CreateVisualEffectObject("spdimndr", Player4)
            CreateVisualEffectObject("spdimndr", Player5)
            CreateVisualEffectObject("spdimndr", Player6)
            SmallWait(15)
            FadeFromColor([30.0],0)
            SmallWait(30)
            EndCutSceneMode()
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../portal_end.baf~
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_PORTAL_CLARITY_SPELL 
    RET clarity_spl
BEGIN
    COPY_EXISTING ~SPPR350.SPL~ ~override/%RESREF_SPL_PORTAL_CLARITY_SUBSPELL%.SPL~
        WRITE_LONG NAME1 0
        WRITE_LONG UNIDENTIFIED_DESC 0
        LPF ADD_SPL_HEADER END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_COLOR_PULSE END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_PLAY_SOUND END
        LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = EFF_OPCODE_GRPAHICS_PLAY_EFFECT END
        BUT_ONLY

    OUTER_SPRINT clarity_spl ~%RESREF_SPL_PORTAL_CLARITY%~

    CREATE ~SPL~ ~%clarity_spl%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_ALTERNATIVE_CHARM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_OPCODE
            parameter1 = "-1"
            parameter2 = EFF_OPCODE_FREEDOM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_FREEDOM
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_CAST_SPELL
            parameter2 = 1 // Cast Instantly at caster level
            STR_VAR resource = ~%RESREF_SPL_PORTAL_CLARITY_SUBSPELL%~
            END
END
