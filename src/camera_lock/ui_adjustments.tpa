DEFINE_ACTION_FUNCTION APPLY_UI_ADJUSTMENTS 
    RET lock_scroll_on_leader_lua_fn
BEGIN
    OUTER_SPRINT lock_scroll_lua_fn ~%LUA_FUNCTION_LOCK_SCROLL%~
    OUTER_SPRINT lock_scroll_on_leader_lua_fn ~%LUA_FUNCTION_LOCK_SCROLL_ON_LEADER%~
    OUTER_SPRINT unlock_scroll_lua_fn ~%LUA_FUNCTION_UNLOCK_SCROLL%~

    COPY_EXISTING ~UI.menu~ ~override~
        LPF APPLY_LOCK_ON_PORTRAIT_DBLCLICK STR_VAR lock_scroll_lua_fn END
        LPF APPLY_LOCK_ON_CENTER_ON_GROUP_LEADER STR_VAR lock_scroll_on_leader_lua_fn END
        LPF APPLY_UNLOCK_ON_SELECT_ALL_ALTCLICK STR_VAR unlock_scroll_lua_fn END
        BUT_ONLY

    COPY ~%COMPONENT_FOLDER%/ui_adjustments.lua~ ~override/%RESREF_LUA_FILE%.lua~
        EVALUATE_BUFFER

    LAF APPLY_HELPTUTORIAL_ADJUSTMENTS END
END

DEFINE_PATCH_FUNCTION APPLY_LOCK_ON_PORTRAIT_DBLCLICK 
    STR_VAR lock_scroll_lua_fn = 0
BEGIN 
    CLEAR_ARRAY portraits
    DEFINE_ARRAY portraits BEGIN 0 1 2 3 4 5 END
    
    PHP_EACH portraits AS _ => portrait BEGIN 
        REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~Infinity_OnPortraitDblClick(%portrait%)~ ~~~~~
            %lock_scroll_lua_fn%()
            Infinity_OnPortraitDblClick(%portrait%)
        ~~~~~
    END
END

DEFINE_PATCH_FUNCTION APPLY_LOCK_ON_CENTER_ON_GROUP_LEADER 
    STR_VAR lock_scroll_on_leader_lua_fn = 0
BEGIN
    REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~game:CenterOnGroupLeader()~ ~~~~~
        %lock_scroll_on_leader_lua_fn%()
        game:CenterOnGroupLeader()
    ~~~~~
END

DEFINE_PATCH_FUNCTION APPLY_UNLOCK_ON_SELECT_ALL_ALTCLICK 
    STR_VAR unlock_scroll_lua_fn = 0
BEGIN
    SET ok = 0
    
    REPLACE_EVALUATE CASE_SENSITIVE ~action%s%*?"%s%*?game:SelectAll()%s%*?"~ 
        BEGIN 
            SET ok = 1
        END ~~~~~
            %MATCH0%
            actionAlt "%unlock_scroll_lua_fn%()"
        ~~~~~
    
    PATCH_IF (NOT ok) BEGIN 
        PATCH_WARN ~Cannot extend "select all" button with "unlock scroll" alt action.~
    END
END

DEFINE_ACTION_FUNCTION APPLY_HELPTUTORIAL_ADJUSTMENTS BEGIN
    OUTER_SPRINT language_lua @1

    COPY_EXISTING ~%language_lua%.LUA~ ~override~
        SPRINT portrait_description @11
        SPRINT portrait_description_extension @12
        SPRINT portrait_description_extended ~%portrait_description%~ ^ ~%portrait_description_extension%~
        REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~%portrait_description%~ ~%portrait_description_extended%~

        SPRINT select_all_description @21
        SPRINT select_all_description_extension @22
        SPRINT select_all_description_extended ~%select_all_description%~ ^ ~%select_all_description_extension%~
        REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~%select_all_description%~ ~%select_all_description_extended%~

        PATCH_IF (IS_EEEX) BEGIN 
            SPRINT select_all_dclick_line @31
            SPRINT select_all_dclick_line_extended @32
            REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~%select_all_dclick_line%~ ~%select_all_dclick_line_extended%~
        END
        BUT_ONLY
END
