DEFINE_ACTION_FUNCTION APPLY_SCRIPTS_ADJUSTMENTS BEGIN

    COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_CUTSCENE_START END
            LPF PATCH_CUTSCENE_END END
        END
        BUT_ONLY

    COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_CUTSCENE_START END
            LPF PATCH_CUTSCENE_END END
        END
        BUT_ONLY

    LAF APPLY_LOCK_ON_GAME_LOAD END
END

DEFINE_PATCH_FUNCTION PATCH_CUTSCENE_START BEGIN
    
    REPLACE_TEXTUALLY EXACT_MATCH ~StartCutSceneMode()~ ~~~~~
        UnlockScroll()
        StartCutSceneMode()
    ~~~~~
END

DEFINE_PATCH_FUNCTION PATCH_CUTSCENE_END BEGIN

    REPLACE_TEXTUALLY EXACT_MATCH ~EndCutSceneMode()~ ~~~~~
        EndCutSceneMode()
        ActionOverride(Player1, LockScroll())
    ~~~~~
END

DEFINE_ACTION_FUNCTION APPLY_LOCK_ON_GAME_LOAD BEGIN
    <<<<<<<< .../scroll_lock.baf
    IF
        OnCreation()
    THEN
        RESPONSE #100
            ActionOverride(Player1, LockScroll())
            Continue()
    END
    >>>>>>>>
    
    EXTEND_TOP ~baldur.bcs~ ~.../scroll_lock.baf~ 
        EVALUATE_BUFFER
END
