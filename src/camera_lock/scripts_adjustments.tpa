DEFINE_ACTION_FUNCTION APPLY_SCRIPTS_ADJUSTMENTS
    STR_VAR lock_scroll_on_leader_lua_fn = 0
BEGIN
    OUTER_SPRINT lock_scroll_action ~~~~~
        ActionOverride(Player1, LockScroll())
    ~~~~~
    OUTER_SPRINT unlock_scroll_action ~~~~~
        ActionOverride(Player1, UnlockScroll())
        ActionOverride(Player2, UnlockScroll())
        ActionOverride(Player3, UnlockScroll())
        ActionOverride(Player4, UnlockScroll())
        ActionOverride(Player5, UnlockScroll())
        ActionOverride(Player6, UnlockScroll())
    ~~~~~

    ACTION_IF (IS_EEEX) BEGIN 
        OUTER_SPRINT lock_scroll_action ~~~~~
            ActionOverride(Player1, EEex_LuaAction("%lock_scroll_on_leader_lua_fn%()"))
            ActionOverride(Player2, EEex_LuaAction("%lock_scroll_on_leader_lua_fn%()"))
            ActionOverride(Player3, EEex_LuaAction("%lock_scroll_on_leader_lua_fn%()"))
            ActionOverride(Player4, EEex_LuaAction("%lock_scroll_on_leader_lua_fn%()"))
            ActionOverride(Player5, EEex_LuaAction("%lock_scroll_on_leader_lua_fn%()"))
            ActionOverride(Player6, EEex_LuaAction("%lock_scroll_on_leader_lua_fn%()"))
        ~~~~~
    END

    COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_CUTSCENE_START STR_VAR unlock_scroll_action END
            LPF PATCH_CUTSCENE_END STR_VAR lock_scroll_action END
        END
        BUT_ONLY

    COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            LPF PATCH_CUTSCENE_START STR_VAR unlock_scroll_action END
            LPF PATCH_CUTSCENE_END STR_VAR lock_scroll_action END
        END
        BUT_ONLY

    LAF APPLY_LOCK_ON_GAME_LOAD STR_VAR lock_scroll_action END
END

DEFINE_PATCH_FUNCTION PATCH_CUTSCENE_START 
    STR_VAR unlock_scroll_action = 0
BEGIN
    REPLACE_EVALUATE CASE_INSENSITIVE ~StartCutSceneMode()~ 
        BEGIN 
            PATCH_LOG ~PATCH_CUTSCENE_START %SOURCE_EXT% %SOURCE_RES%: Replace %MATCH0%~
        END ~~~~~
            %unlock_scroll_action%
            %MATCH0%
        ~~~~~
END

DEFINE_PATCH_FUNCTION PATCH_CUTSCENE_END 
    STR_VAR lock_scroll_action = 0
BEGIN
    REPLACE_EVALUATE CASE_INSENSITIVE ~EndCutSceneMode()~ 
        BEGIN 
            PATCH_LOG ~PATCH_CUTSCENE_END %SOURCE_EXT% %SOURCE_RES%: Replace %MATCH0%~
        END ~~~~~
            %MATCH0%
            %lock_scroll_action%
        ~~~~~
    REPLACE_EVALUATE CASE_INSENSITIVE ~ActionOverride(.*?,EndCutSceneMode())~ 
        BEGIN 
            PATCH_LOG ~PATCH_CUTSCENE_END %SOURCE_EXT% %SOURCE_RES%: Replace %MATCH0%~
        END ~~~~~
            %MATCH0%
            %lock_scroll_action%
        ~~~~~
END

DEFINE_ACTION_FUNCTION APPLY_LOCK_ON_GAME_LOAD 
    STR_VAR lock_scroll_action = 0
BEGIN
    <<<<<<<< .../camera_lock.baf
    IF
        OnCreation()
    THEN
        RESPONSE #100
            %lock_scroll_action%
            Continue()
    END
    >>>>>>>>
    
    LAF EXTEND_GLOBAL_SCRIPT_TOP STR_VAR baf = ~.../camera_lock.baf~  END
END
