DEFINE_ACTION_FUNCTION APPLY_RETURN_CUTSCENES
    STR_VAR master_areas_ref = 0
        portal_markers_ref = 0
        portal_open_variable = 0
        remove_return_spell = 0
        this_cutscene_prefix = 0
BEGIN 
    LAF APPLY_RETURN_CUTSCENE_PREPARATION_SPELL RET preparation_spell END
    LAF APPLY_RETURN_CUTSCENE_FINALIZATION_SPELL STR_VAR preparation_spell
        RET finalization_spell
        END

    <<<<<<<< .../return_cutscene.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)
            ClearAllActions()

            ApplySpell(Player1, DRYAD_TELEPORT)
            ApplySpell(Player2, DRYAD_TELEPORT)
            ApplySpell(Player3, DRYAD_TELEPORT)
            ApplySpell(Player4, DRYAD_TELEPORT)
            ApplySpell(Player5, DRYAD_TELEPORT)
            ApplySpell(Player6, DRYAD_TELEPORT)
  
            SmallWait(45)
            FadeToColor([30.0],0)
            SmallWait(30)

            ApplySpellRES("%preparation_spell%", Player1)
            ApplySpellRES("%preparation_spell%", Player2)
            ApplySpellRES("%preparation_spell%", Player3)
            ApplySpellRES("%preparation_spell%", Player4)
            ApplySpellRES("%preparation_spell%", Player5)
            ApplySpellRES("%preparation_spell%", Player6)

            SmallWait(5)

            LeaveAreaLUAPanic("%master_area_name%", "", [1.1], SSW)
            LeaveAreaLUA("%master_area_name%", "", [1.1] , SSW)
            ActionOverride(Player2, LeaveAreaLUA("%master_area_name%", "", [1.1], SSW))
            ActionOverride(Player3, LeaveAreaLUA("%master_area_name%", "", [1.1], SSW))
            ActionOverride(Player4, LeaveAreaLUA("%master_area_name%", "", [1.1], SSW))
            ActionOverride(Player5, LeaveAreaLUA("%master_area_name%", "", [1.1], SSW))
            ActionOverride(Player6, LeaveAreaLUA("%master_area_name%", "", [1.1], SSW))
            SetMasterArea("%master_area_name%")
            MultiPlayerSync()

            SmallWait(5)

            MoveGlobalObject(Player1, "%portal_marker_dv_p1%") 
            MoveGlobalObject(Player2, "%portal_marker_dv_p2%") 
            MoveGlobalObject(Player3, "%portal_marker_dv_p3%") 
            MoveGlobalObject(Player4, "%portal_marker_dv_p4%") 
            MoveGlobalObject(Player5, "%portal_marker_dv_p5%") 
            MoveGlobalObject(Player6, "%portal_marker_dv_p6%") 

            ActionOverride("%portal_marker_dv_p1%", DestroySelf())
            ActionOverride("%portal_marker_dv_p2%", DestroySelf())
            ActionOverride("%portal_marker_dv_p3%", DestroySelf())
            ActionOverride("%portal_marker_dv_p4%", DestroySelf())
            ActionOverride("%portal_marker_dv_p5%", DestroySelf())
            ActionOverride("%portal_marker_dv_p6%", DestroySelf())

            MoveViewObject(Player6, INSTANT)
            MoveViewObject(Player5, INSTANT)
            MoveViewObject(Player4, INSTANT)
            MoveViewObject(Player3, INSTANT)
            MoveViewObject(Player2, INSTANT)
            MoveViewObject(Player1, INSTANT)

            SetGlobal("%portal_open_variable%", "GLOBAL", 0)
            ApplySpellRES("%remove_return_spell%", Player1)

            ApplySpellRES("%finalization_spell%", Player1)
            ApplySpellRES("%finalization_spell%", Player2)
            ApplySpellRES("%finalization_spell%", Player3)
            ApplySpellRES("%finalization_spell%", Player4)
            ApplySpellRES("%finalization_spell%", Player5)
            ApplySpellRES("%finalization_spell%", Player6)
            
            SmallWait(5)

            CreateVisualEffectObject("spdimndr", Player1)
            CreateVisualEffectObject("spdimndr", Player2)
            CreateVisualEffectObject("spdimndr", Player3)
            CreateVisualEffectObject("spdimndr", Player4)
            CreateVisualEffectObject("spdimndr", Player5)
            CreateVisualEffectObject("spdimndr", Player6)
            SmallWait(15)
            FadeFromColor([30.0],0)
            SmallWait(30)
            EndCutSceneMode()
    END
    >>>>>>>>

    ACTION_PHP_EACH ~%portal_markers_ref%~ AS player_index => portal_marker_dv BEGIN 
        OUTER_SPRINT ~portal_marker_dv_p%player_index%~ ~%portal_marker_dv%~
    END

    ACTION_PHP_EACH ~%master_areas_ref%~ AS master_area_name => master_area_index BEGIN
        OUTER_SPRINT return_cutscene_name ~%this_cutscene_prefix%%master_area_index%~
        
        EXTEND_BOTTOM ~%return_cutscene_name%.BCS~ ~.../return_cutscene.baf~ 
            EVALUATE_BUFFER
    END
END


DEFINE_ACTION_FUNCTION APPLY_RETURN_CUTSCENE_PREPARATION_SPELL 
    RET preparation_spell
BEGIN 
    OUTER_SPRINT preparation_spell ~%RESREF_SPL_RETURN_CUTSCENE_PREPARATION%~

    CREATE ~SPL~ ~%preparation_spell%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_STAT_VISUAL_RANGE
            parameter1 = 0
            parameter2 = 1 // Flat modifier
            END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index
            opcode = EFF_OPCODE_SANCTUARY 
            parameter2 = 1 // Custom overlay
            END
END


DEFINE_ACTION_FUNCTION APPLY_RETURN_CUTSCENE_FINALIZATION_SPELL 
    STR_VAR preparation_spell = 0
    RET finalization_spell
BEGIN 
    OUTER_SPRINT finalization_spell ~%RESREF_SPL_RETURN_CUTSCENE_FINALIZATION%~

    CREATE ~SPL~ ~%finalization_spell%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_EFFECTS_BY_RESOURCE
            STR_VAR resource = ~%preparation_spell%~
            END
END
