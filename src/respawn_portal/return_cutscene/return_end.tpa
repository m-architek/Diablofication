DEFINE_ACTION_FUNCTION APPLY_RETURN_END
    STR_VAR return_begin_spell = 0
        portal_marker_dv = 0
        close_portal_action = 0
        this_cutscene = 0
BEGIN 
    LAF APPLY_RETURN_END_SPELL STR_VAR return_begin_spell
        RET return_end_spell
        END

    <<<<<<<< .../return_end.baf
    IF
        True()
    THEN
        RESPONSE #100
            CutSceneId(Player1)

            MoveGlobalObject(Player1, "%portal_marker_dv%") 
            MoveGlobalObject(Player2, "%portal_marker_dv%") 
            MoveGlobalObject(Player3, "%portal_marker_dv%") 
            MoveGlobalObject(Player4, "%portal_marker_dv%") 
            MoveGlobalObject(Player5, "%portal_marker_dv%") 
            MoveGlobalObject(Player6, "%portal_marker_dv%") 
            
            MoveViewObject("%portal_marker_dv%", INSTANT)
            ActionOverride("%portal_marker_dv%", DestroySelf())

            %close_portal_action%

            ApplySpellRES("%return_end_spell%", Player1)
            ApplySpellRES("%return_end_spell%", Player2)
            ApplySpellRES("%return_end_spell%", Player3)
            ApplySpellRES("%return_end_spell%", Player4)
            ApplySpellRES("%return_end_spell%", Player5)
            ApplySpellRES("%return_end_spell%", Player6)

            SetSequence(SEQ_READY)
            ActionOverride(Player2, SetSequence(SEQ_READY))
            ActionOverride(Player3, SetSequence(SEQ_READY))
            ActionOverride(Player4, SetSequence(SEQ_READY))
            ActionOverride(Player5, SetSequence(SEQ_READY))
            ActionOverride(Player6, SetSequence(SEQ_READY))
            
            ApplySpell(Player1, DRYAD_TELEPORT)
            ApplySpell(Player2, DRYAD_TELEPORT)
            ApplySpell(Player3, DRYAD_TELEPORT)
            ApplySpell(Player4, DRYAD_TELEPORT)
            ApplySpell(Player5, DRYAD_TELEPORT)
            ApplySpell(Player6, DRYAD_TELEPORT)
            
            SmallWait(15)
            FadeFromColor([30.0],0)
            SmallWait(30)
            EndCutSceneMode()
    END
    >>>>>>>>

    EXTEND_BOTTOM ~%this_cutscene%.BCS~ ~.../return_end.baf~ 
        EVALUATE_BUFFER
END


DEFINE_ACTION_FUNCTION APPLY_RETURN_END_SPELL 
    STR_VAR return_begin_spell = 0
    RET return_end_spell
BEGIN 
    OUTER_SPRINT return_end_spell ~%RESREF_SPL_RETURN_END%~

    CREATE ~SPL~ ~%return_end_spell%~
        LPF ADD_SPL_ABILITY RET ability_index END
        LPF ADD_SPL_ABILITY_EFFECT INT_VAR ability_index 
            opcode = EFF_OPCODE_REMOVE_EFFECTS_BY_RESOURCE
            STR_VAR resource = ~%return_begin_spell%~
            END
END
